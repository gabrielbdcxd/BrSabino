//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: gatuno.txt                             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: ??/1.5/1.0                          |
//   |--------------------------------------------------------|
//   | Descrição: Tutorial da classe gatuno.                  |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

// Thief Trainer
//============================================================
moc_ruins,66,164,4	script	Thief Trainer#T	4_M_02,{
	mes "[Yierhan]";
	if (Class == Job_Novice){
		mes "Eh...?";
		mes "Um noviço?";
		mes "Ainda pensando";
		mes "qual a classe que você";
		mes "irá escolher...";
		mes "Estou certo?";
		next;
		mes "[Yierhan]";
		mes "Escute, se você decidir que quer se tornar um Gatuno--é uma escolha inteligente";
		mes "Posso mudar--venha falar comigo. Vou mostrar-lhe todos os passos!";
		close;

	} else if (Class == Job_Thief || Class == Job_Thief_High || Class == Job_Assassin || Class ==Job_Assassin_Cross || Class == Job_Rogue || Class == Job_Stalker){
		if (Class == Job_Thief_High || Class == Job_Assassin || Class == Job_Assassin_Cross || Class == Job_Rogue || Class == Job_Stalker){
			if (tu_thief01 < 8){
				mes "O queee você está";
				mes "fazendo aqui? Não há";
				mes "nada que eu possa ensinar-lhe!";
				mes "Você está muuuito além de mim!";
				next;
				mes "[Yierhan]";
				mes "Na verdade, eu acho";
				mes "que você está qualificado";
				mes "para me ensinar algumas coisas!";
				mes "Vamos lá! Preciso de novos movimentos!";
				close;
			}
		}
		if (tu_thief01 == 0){
			mes "Heei Amigo.";
			mes "Eu sou Yierhan.";
			mes "Acontece que sou";
			mes "o cara no comando";
			mes "de formação de novos Gatunos.";
			next;
			switch(select("Treinamento?:Treinamento? Agora?")){
			case 1:
				mes "[Yierhan]";
				mes "Sim, treinamento. Quero dizer, esse tipo de coisa é uma segunda natureza para algumas pessoas, mas outros precisam de um pouco mais de ajuda. Portanto, este é um 'daqueles caso' de coisas.";
				next;
				break;
			case 2:
				mes "[Yierhan]";
				mes "Agora?";
				mes "Sim, agora mesmo!";
				mes "Mas se você não estiver pronto, por algum motivo, acredito que posso esperar.";
				close;
			}
			mes "[Yierhan]";
			mes "Tudo bem, em primeiro lugar, Gatunos usam ataques corpo a corpo. Bem, a maioria de nós faz isso. Há alguns que gostam de usar arcos de longo alcance. Mas todos nós ágeis o suficiente!";
			next;
			mes "[Yierhan]";
			mes "Então, para ataques corpo a corpo, o que aumenta o seu status de dano? Vamos lá!";
			mes "Você deveria saber isso se é que não pulou o Treinamento de Campo para Noviço.";
			next;
			switch(select("^6B8E23INT^000000:^2F4F2FSTR^000000:^23238EDEX^000000")){
			case 1:
				mes "[Yierhan]";
				mes "Dizer o queee? ^6B8E23INT^000000 afeta dano mágico, defesa mágica e algumas habilidades. Gatunos não usam magias!";
				next;
				mes "[Yierhan]";
				mes "Você deve ter pulado o Treinamento de Campo para Noviço completamente! Não que eu esteja culpando você. Enfim, é ^2F4F2FSTR^000000 que aumenta seu dano de ataque entendeu?";
				next;
				mes "[Yierhan]";
				mes "Então, se você estiver sentindo que o dano que você está tirando está muito fraco, poderá aumentar seus pontos de status ^2F4F2FSTR^000000. Quanto mais alto seu pontos em STR maior será seu dano.";
				set tu_thief01, 1;
				getexp 200,100;
				specialeffect2 EF_HIT5;
				close;
			case 2:
				mes "[Yierhan]";
				mes "Sim, está certo! se você quiser aumentar seu dano, precisará";
				mes "colocar alguns pontos de status em^2F4F2FSTR^000000. Aumentando os pontos tambem aumantará sua";
				mes "sua capacidade de carga ao limite máximo.";
				next;
				mes "[Yierhan]";
				mes "Então, se você estiver sentindo que o dano que você está muito fraco, poderá aumentar seus pontos de status ^2F4F2FSTR^000000. Quanto mais alto seus pontos em STR maior será seu dano.";
				next;
				mes "[Yierhan]";
				mes "É claro, ^23238EDEX^000000 e LUK você poderá";
				mes "aumentar o seu dano de ataque, mas eles são insignificantes comparados com STR. Repito: ^660000insignificante^000000.";
				set tu_thief01, 1;
				getexp 400,200;
				specialeffect2 EF_HIT5;
				close;
			case 3:
				mes "[Yierhan]";
				mes "Dizer o queee? ^23238EDEX^000000 afeta";
				mes "sua precisão ataque, não o dano! A menos que você esteja usando";
				mes "um Arco. Caso contrário, ele aumenta seu dano só por um pouquinho.";
				next;
				mes "[Yierhan]";
				mes "Você deve ter ignorado o Treinamento de Campo para Noviço completamente! Não que eu esteja culpando você. Enfim, é ^2F4F2FSTR^000000 que aumenta o seu dano de ataque, entendeu??";
				next;
				mes "[Yierhan]";
				mes "Então, se você estiver sentindo que o dano que você está muito fraco, poderá aumentar seus pontos de status ^2F4F2FSTR^000000. Quanto mais alto seus pontos em STR maior será seu dano.";
				set tu_thief01, 1;
				getexp 200,100;
				specialeffect2 EF_HIT5;
				close;
			}
		} else if (tu_thief01 == 1){
			mes "Tudo bem, vamos falar sobre Status.";
			mes "Você sabe o que é? Acho que eu só vou falar sobre as habilidades que nós Gatunos usamos.";
			next;
			mes "[Yierhan]";
			mes "Portando nivele suas habilidades, aprenda algunas novas, se quiser, e";
			mes "em seguida volte.";
			set tu_thief01, 2;
			close;
		} else if (tu_thief01 == 2){
			mes "Ok, deixe-me ver suas habilidades. Você sabe que tem que mudar a sua estratégia de batalha dependendo das habilidades que você tem, certo? Habilidades são tão importantes como o Status!";
			next;
			set .@chk_th_skill1, getskilllv("TF_DOUBLE");
			set .@chk_th_skill2, getskilllv("TF_MISS");
			set .@chk_th_skill3, getskilllv("TF_STEAL");
			set .@chk_th_skill4, getskilllv("TF_HIDING");
			set .@chk_th_skill5, getskilllv("TF_POISON");
			set .@chk_th_skill6, getskilllv("TF_DETOXIFY");

			if (.@chk_th_skill1 == 0 && .@chk_th_skill2 == 0 && .@chk_th_skill3 == 0 && .@chk_th_skill4 == 0 && .@chk_th_skill5 == 0 && .@chk_th_skill6 == 0){
				mes "[Yierhan]";
				mes "Não aprendeu nenhuma habilidade ainda? Vamos lá, são suas habilidades que irão diferencia-lo dos Noviços e de todos os outros!";
				close;
			} else {
				if (.@chk_th_skill1 > 0){
					mes "[Yierhan]";
					mes "Ah, então você aprendeu";
					mes "Level "+.@chk_th_skill1+" Ataque Duplo.";
					mes "Legal! Esta habilidade lhe dá a chance de atacar duas vezes";
					mes "em um único ataque. Perverso!";
					next;
					mes "[Yierhan]";
					mes " Ela é uma habilidade passiva, por isso é sempre estará em vigor e não terá que gastar SP usá-lo. Quanto maior o seu nível de habilidade em Ataque Duplo, mais vezes você poderá usar.";
					next;
				}
				if (.@chk_th_skill2 > 0){
					mes "[Yierhan]";
					mes "Vejamos...";
					mes "Nivel "+.@chk_th_skill2+" Aumentar Esquiva?";
					mes "Isso aumenta sua taxa, significa que você tem uma melhor chance de evitar ataques de seus inimigos.";
					next;
					mes "[Yierhan]";
					mes "Assim como a habilidade Ataque Duplo, Aumentar Esquiva é uma habilidade passiva. Não usará SP e é sempre no";
					mes "efeito. Se você não gosta de se machucar, esta é a sua habilidade.";
					next;
				}
				if (.@chk_th_skill3 > 0){
					mes "[Yierhan]";
					mes "Whoa, então você";
					mes "já tem nivel "+.@chk_th_skill3+" Steal~";
					mes "Agora veremos a habilidade que confere à nossa classe o seu nome! Você poderá usar";
					mes "contra pessoas, apesar de...";
					next;
					mes "[Yierhan]";
					mes "Você pode usar Furto para pegar os ítens dos monstros. Se tiver sorte, poderá obter alguns itens bons. Ahh Furtar não afeta a taxa de evasão dos montros.";
					next;
				}
				if (.@chk_th_skill4 > 0){
					mes "[Yierhan]";
					mes "Você aprendeu";
					mes "Nivel "+.@chk_th_skill4+" Esconderijo?";
					mes "Deixe-me ver, você só poderá aprender";
					mes "que depois de tiver a habilidade Furto até um certo nível.";
					next;
					mes "[Yierhan]";
					mes "Naturalmente, você usa ";
					mes "a habilidade Esconderijo para se esconder no subsolo em uma emergência, como quando você está cercado por inimigos difíceis. Seja cuidadoso, porém...";
					next;
					mes "[Yierhan]";
					mes "Alguns monstros ainda serão capazes de encontrá-lo, não importa o quão bem você esteja escondido! Há alguns monstros que podem fazer com que você saia do esconderijo!";
					next;
				}
				if (.@chk_th_skill5 > 0){
					mes "[Yierhan]";
					mes "Tudo bem, vejo que você";
					mes "Já conhece Nivel "+.@chk_th_skill5+" Envenenar.";
					mes "Você gosta de ser perigoso,";
					mes "não é mesmo?";
					next;
					mes "[Yierhan]";
					mes "Esta habilidade de ataque lhe dá a chance";
					mes "de envenenar seu inimigo por um determinado período de tempo. Enquanto envenenado, um inimigo constantemente perder o seu HP e terá diminuída sua defesa.";
					next;
					mes "[Yierhan]";
					mes "Eh, mas lembre-se.";
					mes "Se o monstro for muito forte, você pode não ser capaz de envenenar-lo. Portanto, não enlouqueça.";
					next;
				}
				if (.@chk_th_skill6 > 0){
					mes "[Yierhan]";
					mes "Level "+.@chk_th_skill6+" Detoxify.";
					mes "Se o monstro for muito forte, você pode não ser capaz de envenenar-lo. Portanto, não enlouqueça";
					mes "ou algo assim.";
					next;
					mes "[Yierhan]";
					mes "Você só pode aprender Desintoxicar";
					mes "depois que você aprender a habilidade Envenenar. Desintoxicar permite neutralizar os efeitos do veneno em um alvo.";
					next;
				}
			}
			mes "[Yierhan]";
			mes "Tudo bem, eu acho";
			mes "se você quiser saber ";
			mes "sobre quaisquer outras habilidades,";
			mes "Eu posso explicar bem rápido.";
			set tu_thief01, 3;
			getexp BaseLevel*30,BaseLevel*15;
			specialeffect2 EF_HIT5;
			close;
		} else if (tu_thief01 == 3){
			mes "So...";
			mes "Existe alguma";
			mes "habilidade que você queira";
			mes "que eu explique ou talvez";
			mes "você já esteja sabendo";
			mes "quase tudo?";
			next;
			while(1){
				switch(select("Ataque Duplo:Aumentar Esquiva:Furto:Hiding:Envenenar:Desintoxicar:Eu sei o suficiente.")) {
				case 1:
					mes "[Yierhan]";
					mes "Assim como nome diz,";
					mes "Ataque Duplo dá aos seus ataques a chance de atacar duas vezes, dois danos em um golpe.";
					next;
					mes "[Yierhan]";
					mes "Ela é uma habilidade passiva, por isso é sempre estará em vigor e não terá que gastar SP usá-lo. Quanto maior o seu nível de habilidade em Ataque Duplo, mais vezes você poderá usar.";
					set .@read_d, 1;
					next;
					break;
				case 2:
					mes "[Yierhan]";
					mes "Aumentar Esquiva";
					mes "dá um bom impulso";
					mes "na sua taxa de evasão de ataques físicos";
					mes "e que as outras classes não oferecem.";
					mes "Por que ficar recebendo danos quando";
					mes "pode evitá-los completamente?";
					next;
					mes "[Yierhan]";
					mes "Assim como a habilidade Ataque Duplo, Aumentar a Esquiva é uma habilidade passiva. Não usará SP e ela sempre atua";
					mes "no efeito. Se você não gosta de se machucar, esta é a sua habilidade.";
					set .@read_f, 1;
					next;
					break;
				case 3:
					mes "[Yierhan]";
					mes "Furtar é uma habilidade ativa em que tem a chance afanar alguns";
					mes "ítens de graça! Porém, você não pode usá-lo contra outras pessoas.";
					next;
					specialeffect2 EF_STEAL;
					mes "[Yierhan]";
					mes "Mas você pode usar Furtar para pegar itens de monstros! Se você tiver sorte, poderá obter alguns itens bons. Ah, e roubar não afeta os danos nos monstro.";
					set .@read_s, 1;
					next;
					break;
				case 4:
					mes "[Yierhan]";
					mes "Esconderijo é uma habilidade ativa onde você pode submergir-se no subsolo. Você só pode ficar escondido por algum tempo, e não poderá se mover, mas às vezes é melhor do que ser encontrado!";
					next;
					mes "[Yierhan]";
					mes "É claro, você poderá usar";
					mes "a habilidade Esconderijo para se ocultar no subsolo no caso de emergência, como quando você está cercado por inimigos difíceis. Porém, tenha muito cuidado...";
					next;
					mes "[Yierhan]";
					mes "Alguns monstros serão capazes de encontrá-lo, não importa o quão bem você estiver escondido. Há mesmo alguns monstros que você podem retirar você do seu esconderijo!";
					set .@read_p, 1;
					next;
					break;
				case 5:
					mes "[Yierhan]";
					mes "Envenenar é uma habilidade ativa ofensiva que todo Gatuno deveria saber. Pelo menos é o que imagino.";
					next;
					mes "[Yierhan]";
					mes "Esta habilidade dá a chance";
					mes "de envenenar seu inimigo por um determinado período de tempo. Enquanto envenenado, o inimigo perde constantemente seu HP e terá sua de defesa diminuida.";
					mes "Lembre-se sempre disso.";
					set .@read_h, 1;
					next;
					break;
				case 6:
					mes "[Yierhan]";
					mes "Uma vez que Gatunos negociam";
					mes "bastante com venenos,";
					mes "nós temos que ter uma maneira";
					mes "de poder provar";
					mes "nosso próprio remédio.";
					next;
					mes "[Yierhan]";
					mes "Você deverá aprender sobre Desintoxicar";
					mes "depois que aprender a habilidade Envenenar. Desintoxicar permite neutralizar os efeitos do veneno em um alvo.";
					set .@read_r, 1;
					next;
					specialeffect2 EF_DETOXICATION;
					next;
					break;
				case 7:
					mes "[Yierhan]";
					mes "Sim...";
					mes "De todo jeito, estou cansado de explicar estas habilidades. Vamos seguir em frente para";
					mes "a próxima parte do treinamento de Gatuno...";
					next;
					mes "[Yierhan]";
					mes "Agora que você é um especialista nas habilidades, quero que você suba de nível e volte aqui, entendeu?";
					set tu_thief01, 4;
					specialeffect2 EF_HIT5;
					if (.@read_d) getexp 300,100;
					if (.@read_f) getexp 300,100;
					if (.@read_s) getexp 300,100;
					if (.@read_p) getexp 300,100;
					if (.@read_h) getexp 300,100;
					if (.@read_r) getexp 300,100;
					close;
				}
			}
		} else if (tu_thief01 == 4){
			mes "Tudo bem, nós estudamos as habilidades e você tem praticado bastante, certo? Melhor você ter...";
			next;
			mes "[Yierhan]";
			mes "Tudo bem, eu tenho alguma formação em luta real quero que você treine. Aqui está uma oportunidade para você descobrir quais habilidades são os melhores para várias situações.";
			next;
			mes "[Yierhan]";
			mes "Vá e me traga";
			mes "10 ^ff0000Pluma de Ave^000000.";
			mes "Você pode ir em frente e matar Picki para obter as plumas. Realmente não deve ser tão difícil. Ah, pode usar essas Asas de Mosca para voltar.";
			set tu_thief01, 5;
			savepoint "moc_ruins",80,164;
			getitem 602,1; //Wing_Of_Butterfly
			getexp 100,50;
			specialeffect2 EF_HIT5;
			close2;
			warp "moc_fild12",158,373;
			end;
		} else if (tu_thief01 == 5 || tu_thief01 == 6){
			if (countitem(916) < 10){
				mes "'Ei, você não tem as 10 ^ff0000Pluma de Ave^000000 Foi o que lhe pedi! Você tem que aplicar o que aprendeu.";
				mes "Agora apresse-se em fazer isso!";
				close2;
				warp "moc_fild07",203,38;
				end;
			} else {
				mes "Tudo bem...!";
				mes "Bom trabalho, amigo.";
				mes "Vejo que você tem tem todas as penas, você deve ser muito entusiasta sobre como se tornar um bom Gatuno.";
				next;
				mes "[Yierhan]";
				mes "Eu espero que você continue";
				mes "focando no seu trabalho";
				mes "para que fique cada vez melhor.";
				mes "Sempre dê o melhor de sí! Oh,";
				mes "você tem mais alguma dúvida?";
				next;
				if (tu_thief01 == 6){
					switch(select("Sobre esses traços...:Não.")) {
 					case 1:
						mes "[Yierhan]";
						mes "Você descobriu, hein?";
						mes "Bem, eu realmente não ";
						mes "quero dizer-lhe isso, em";
						mes "consideração por você estar";
						mes "com uma nova marca de Gatuno, mas...";
						next;
						mes "[Yierhan]";
						mes "Haviam alguem";
						mes "um lutadore que estava";
						mes "na parte sul";
						mes "desta cidade.";
						next;
						mes "[Yierhan]";
						mes "Desde que isso aconteceu tarde da noite, poucas pessoas realmente sabem algo sobre ele. Quanto a mim, eu ficava até mais tarde nas reuniões de alianças, por isso foi";
						mes "pura sorte que eu ter visto.";
						next;
						mes "[Yierhan]";
						mes "Voltei para onde a luta havia acontecido e encontrei traços que mostravam que os caras que estavam lutando foram para o sul.";
						next;
						mes "[Yierhan]";
						mes "Uma vez que o veneno foi utilizado";
						mes "nesta luta, imagino que um assassino estava envolvido, mas posso não estar certo. Se quiser dar uma olhada, basta seguir os rastros da luta.";
						next;
						mes "[Yierhan]";
						mes "Eh, mas tenha cuidado";
						mes "para não chegar muito perto";
						mes "do veneno! Essas coisas";
						mes "costumam ser fortes!";
						set tu_thief01, 8;
						getitem 1207,1; //Main_Gauche
						getexp 1000,500;
						specialeffect2 EF_HIT5;
						close;
					case 2:
						mes "[Yierhan]";
						mes "Bem!";
						mes "Menos trabalho pra mim!";
						mes "Tudo bem, será melhor ficar mais forte na próxima vez que nos vermos. Ah, e você poderá ter essas coisas. Espero que entenda, já que você é tão gangster.";
						set tu_thief01, 7;
						getitem 1207,1; //Main_Gauche
						getexp 500,200;
						specialeffect2 EF_HIT5;
						close;
					}
				} else {
					switch(select("Foi bom conhecê-lo.:Não.")) {
					case 1:
						mes "[Yierhan]";
						mes "Sim, foi muito legal apenas sair. Continue lutando";
						mes "com monstros do jeito que você faz e ";
						mes "irá se tornar um grande Gatuno algum dia.";
						next;
						mes "[Yierhan]";
						mes "Desde que gosto";
						mes "muito de você,garoto";
						mes "vou lhe dar algo. Tome,";
						mes "fique com isso, é pra você!";
						break;
					case 2:
						mes "[Yierhan]";
						mes "Bem!";
						mes "Menos trabalho pra mim!";
						mes "Tudo bem, será melhor ficar mais forte na próxima vez que nos vermos. Ah, e você poderá ter essas coisas. Espero que entenda, já que você é tão gangster.";
						break;
					}
					set tu_thief01, 7;
					getitem 1207,1; //Main_Gauche
					getexp 500,200;
					specialeffect2 EF_HIT5;
					close;
				}
			}
		} else if (tu_thief01 == 7){
			mes "[Yierhan]";
			mes "Você sabe...";
			mes "Haviam alguem";
			mes "um lutadore que estava";
			mes "na parte sul";
			mes "desta cidade.";
			next;
			mes "[Yierhan]";
			mes "Desde que isso aconteceu tarde da noite, poucas pessoas realmente sabem algo sobre ele. Quanto a mim, eu ficava até mais tarde nas reuniões de alianças, por isso foi";
			mes "pura sorte que eu ter visto..";
			next;
			mes "[Yierhan]";
			mes "Voltei para onde a luta havia acontecido e encontrei traços que mostravam que os caras que estavam lutando foram para o sul.";
			next;
			mes "[Yierhan]";
			mes "Uma vez que o veneno foi utilizado";
			mes "nesta luta, imagino que um assassino estava envolvido, mas posso não estar certo. Se quiser dar uma olhada, basta seguir os rastros da luta.";
			next;
			mes "[Yierhan]";
			mes "Eh, mas tenha cuidado";
			mes "para não chegar muito perto";
			mes "do veneno! Essas coisas";
			mes "costumam ser fortes!";
			next;
			mes "[Yierhan]";
			mes "Hey, você poderá se proteger de";
			mes "envenenamentos, basta lembrar de usar";
			mes "Ervas Verdes e Poções Verdes";
			mes "para anular seu efeito.  Ah, e lembre-se de que as";
			mes "Gemas Vermelhas podem ser usadas para auxiliar em ataques de veneno.";
			next;
			mes "[Yierhan]";
			mes "Ei, se você estiver";
			mes "decidido a sair para verificar";
			mes "por aí afora, tome cuidado";
			mes "com estranhos, ok?";
			set tu_thief01, 8;
			getexp 200,100;
			specialeffect2 EF_HIT5;
			close;
		} else if (tu_thief01 == 8){
			mes "Hei amigo.";
			mes "Está tudo bem?";
			next;
			mes "[Yierhan]";
			mes "Lutar contra algo";
			mes "do qual não sabe nada é sempre arriscado. Desde que ouvi falar que há estranhos ao redor ostentando seu poder, fiquei mais cauteloso.";
			next;
			mes "[Yierhan]";
			mes "É uma boa política ser muito cuidado em suas aventuras. Fique atento com os monstros";
			mes "e procure por pessoas! Entendido?";
			close;
		} else if (tu_thief01 < 26){
			mes "Eu soube que havia";
			mes "um famoso Assassino";
			mes "que saiu em uma missão";
			mes "e nunca mais retornou.";
			next;
			mes "[Yierhan]";
			mes "O que parece mais estranho nisso é que a Guilda dos Assassinos informou que a missão fora concluída!";
			mes "Eu lembro de ter ouvido que esse cara";
			mes "era realmente bom...";
			next;
			mes "[Yierhan]";
			mes "Loucura, não é mesmo?";
			mes "De qualquer forma é melhor";
			mes "tomar muito cuidado.";
			close;
		} else if (tu_thief01 == 26){
			mes "'Ei, você ainda não";
			mes "completou sua missão?";
			mes "Eu sei, eu sei, esta é a única coisa que você tem que fazer mas é muito difícil.";
			next;
			switch(select("Eu ainda estou investigando.:Ainda não.:Sim, eu fiz.")) {
			case 1:
				mes "[Yierhan]";
				mes "Ah, te peguei. Bem, isso é compreensível. Quero dizer, essas coisas levam tempo, sabe?";
				close;
			case 2:
				mes "[Yierhan]";
				mes "Pois é...?";
				mes "Está certo.";
				mes "Eu acho que esse tipo de coisa requer paciência. E muita inteligência.";
				mes "Você sabe, eu sou horrivelmente exigente.";
				close;
			case 3:
				mes "[Yierhan]";
				mes "Tudo bem...!";
				mes "Então, o que há?";
				mes "conte-me tudo do que";
				mes "você está sabendo~";
				next;
				mes "^3355FFVocê explica os resultados de sua investigação para Yierhan e fala sobre um pedaço de pano que encontrou em sua missão.^000000";
				next;
				mes "[Yierhan]";
				mes "Um pedaço de pano?";
				mes "Hum, tudo bem. Vejamos,";
				mes "quero dar uma olhada nisso.";
				next;
				mes "[Yierhan]";
				mes "Uaau!";
				mes "Você se saiu muito bem.";
				mes "Esta é uma informação";
				mes "importantíssima!";
				next;
				mes "[Yierhan]";
				mes "Você vê que o padrão sobre o pano é uma espécie de códigos que os Assassinos usam. Este padrão particular parece pertencer aos assassinos mais elevados.";
				next;
				mes "[Yierhan]";
				mes "Eu seria capaz de descobrir o que aconteceu naquela noite, com as novas informações que este pano pode me dar. Muito obrigado!";
				next;
				mes "[Yierhan]";
				mes "De agora em diante,";
				mes "os líderes encarregados";
				mes "da Guilda irão assumir a investigação. Você fez seu trabalho perfeitamente, por isso é hora de aprimorar suas habilidades.";
				next;
				mes "[Yierhan]";
				mes "Hei, este material é seu. Pense nisso como uma recompensa por nos ajudar. Cuide-se agora~";
				set tu_thief01, 27;
				getitem 2307,1; //Mantle
				set Zeny, Zeny + 5000;
				getexp 8000,3000;
				close;
			}
		} else {
			mes "[Yierhan]";
			mes "Hei...";
			mes "Você tem sonhos,";
			mes "não é? Eu sei,";
			mes "é bastante profundo esse";
			mes "assunto que acaei puxando";
			mes "do nada, mas...";
			next;
			mes "[Yierhan]";
			mes "Só gostaria de dizer para as pessoas que sigam seus sonhos. Então faça isso. A vida sem sonhos é muito chata, você não acha?";
			close;
		}
	} else if (Class == Job_Mage || Class == Job_Mage_High || Class == Job_Wizard || Class == Job_High_Wizard || Class == Job_Sage || Class == Job_Professor){
		if (tu_magician01 < 7){
			mes "[Yierhan]";
			mes "Heya.";
			mes "Eu sou o treinador de Gatunos daqui. Claro, parece que não tem nada em comum, mas...";
			next;
			mes "[Yierhan]";
			mes "Na verdade, tenho alguns amigos usando magia aqui e ali. Esse material místico é muuuito complicado pra mim, mas";
			mes "Eu tenho muito respeito por isso.";
			close;
		} else if (tu_magician01 == 7){
			mes "Hum...?";
			mes "Isso é estranho, normalmente";
			mes "Gatunos perdem tempo com essas articulações. O que alguém como você esta";
			mes "fazendo aqui? A menos que...";
			next;
			switch(select("Estou aqui em nome de Mana.'")) {
			case 1:
				mes "[Yierhan]";
				mes "Certo, você deve ser a ajuda que Mana iria enviar! Você chegou no momento certo. Veja, nós encontramos algo estranho ao sul de Morroc.";
				next;
				mes "[Yierhan]";
				mes "Basicamente, encontramos vestígios de veneno que foram usados ??em uma luta. Estávamos indo para investigá-lo, mas temos sido inundados com todo este tipo de trabalho.";
				next;
				mes "[Yierhan]";
				mes "É por isso que estamos pedindo";
				mes "ajuda para a Guilda dos Magos. Felizmente, tenho amigos com Mana, tão...";
				next;
				mes "[Yierhan]";
				mes "De qualquer forma, siga para o";
				mes "Sul Morroc já que parece ser um bom lugar para começar a investigar. Você verá o que encontramos em frente ao portão do Sul Morroc.";
				set tu_magician01, 8;
				break;
			}
		} else if (tu_magician01 < 26){
			mes "Eu soube que havia";
			mes "um famoso Assassino";
			mes "que saiu em uma missão";
			mes "e nunca mais voltou.";
			next;
			mes "[Yierhan]";
			mes "O que é mais estranho é que a Aliança dos Assassinos informou que a missão foi concluída!";
			mes "Me lembro de ter ouvido falar desse cara,";
			mes "ele era muito bom...";
			next;
			mes "[Yierhan]";
			mes "Tenha cuidado, amigo.";
			mes "Parece que sua";
			mes "investigação";
			mes "pode estar relacionado com";
			mes "aquele misterioso assassino...";
			close;
		} else if (tu_magician01 == 26){
			mes "Então, como está";
			mes "a investigação?";
			mes "Está indo bem?";
			mes "Tenho estado tão ocupado,";
			mes "que não conseguia me concentrar";
			mes "nela e nas outras coisas...";
			next;
			switch(select("Eu ainda estou investigando.:Eu não estou pronto ainda...:Oh, eu terminei~")) {
			case 1:
				mes "[Yierhan]";
				mes "Ah, te peguei. Bem, isso é compreensível. Quero dizer, essas coisas levam tempo, entende?";
				close;
			case 2:
				mes "[Yierhan]";
				mes "Sim...?";
				mes "Tudo bem.";
				mes "Eu acho que esse tipo de coisa requer paciência. E muita inteligência.";
				mes "Você sabe, eu sou horrivelmente exigente.";
				close;
			case 3:
				mes "[Yierhan]";
				mes "Tudo bem...!";
				mes "Então, o que há?";
				mes "conte-me tudo do que";
				mes "você está sabendo~";
				next;
				mes "^3355FF30 Você explica os resultados de sua investigação para Yierhan e fala sobre um pedaço de pano que encontrou em sua missão.^000000";
				next;
				mes "[Yierhan]";
				mes "Um pedaço de pano?";
				mes "Hum, tudo bem. Vejamos,";
				mes "quero dar uma olhada nisso.";
				next;
				mes "[Yierhan]";
				mes "Uaau!";
				mes "Você se saiu muito bem.";
				mes "Esta é uma informação";
				mes "importantíssima!";
				next;
				mes "[Yierhan]";
				mes "Você vê que o padrão sobre o pano é uma espécie de códigos que os Assassinos usam. Este padrão particular parece pertencer aos assassinos mais elevados.";
				next;
				mes "[Yierhan]";
				mes "Eu seria capaz de descobrir o que aconteceu naquela noite, com as novas informações que este pano pode me dar. Muito obrigado!";
				next;
				mes "[Yierhan]";
				mes "De agora em diante,";
				mes "os líderes encarregados";
				mes "da Guilda irão assumir a investigação. Você fez seu trabalho perfeitamente, por isso é hora de aprimorar suas habilidades.";
				next;
				mes "[Yierhan]";
				mes "Hei, este material é seu. Pense nisso como uma recompensa por nos ajudar. Cuide-se agora~";
				set tu_magician01, 27;
				set Zeny, Zeny + 5000;
				getexp 5000,2000;
				close;
			}
		} else {
			mes "Exatamente como eu pensei,";
			mes "mais de um grupo";
			mes "estava envolvido nisso tudo.";
			mes "Humm. Algo muito ruim";
			mes "poderá acontecer em breve...";
			close;
		}
	}
	mes "Algumas pessoas pensam";
	mes "que o deserto é apenas um";
	mes "um lugar perigoso e desconfortável";
	mes "onde ninguém quer estar.";
	mes "Mas não é so isso,";
	mes "existem muitas coisas boas aqui.";
	next;
	mes "[Yierhan]";
	mes "Temos o calor escaldante que incentiva o uso de trajes sumários, temos... Cactos. Temos mais areia do que numa praia. Humm ... Tempestades de areia?";
	close;
}