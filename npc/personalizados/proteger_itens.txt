//===== Hercules Script =======================================
//= Item Bind Script
//===== By: ==================================================
//= Akinari
//Esta é uma tradução do npc original feita para o forum do brathena
//By rexxona 
//===== Compatível Com: ==========================================
//= BRathena -- Tradução corrigida por: Julinhobrow
//===== Compatible With: =====================================
//= Revision 17351+ (rAthena)
//= Revision 12949+ (Hercules)
//===== Description: =========================================
//= Item Bind Script
//= Allows users to pay a price to make an item bound to
//= Account, Character, or Guild
//============================================================

prontera,147,175,6	script	Proteção de Itens	4_M_JP_MID,{

	mes "Eu posso vincular os itens para a sua conta, clã ou personagem"+((.bindprice)?" pelo valor de ^0000FF"+.bindprice+"^000000 zeny":"")+".";
	next;
	mes "Com isso, você pode está certo de seus itens estão seguros.";
	next;
	mes "O que você gostaria de fazer?";
	if(select("Vincular:Desvincular") == 1) {
		if(Zeny < .bindprice) {
			mes "Você não tem Zeny suficiente para vincular um item.";
			close;
		}
		mes "Que tipo de vinculo?";
		.@boundtype = 1 << (select("Conta:Clã:Personagem")-1);
		if(.@boundtype == 2 && (!getcharid(2) || getguildmaster(getcharid(2)) != strcharinfo(0))) {
			mes "Para que eu possa vincular um item a um clã você tem que ser o lider";
			close;
		}
		getinventorylist();
		for(.@i = 0; .@i < @inventorylist_count; .@i++) {
			//We only show the items that you allow to be bound
			//Allows equipment (default) or non-rental item
			if( @inventorylist_bound[.@i] || @inventorylist_expire[.@i] )
				continue;
			if(((.allowbind & 1) && (getiteminfo(@inventorylist_id[.@i],2) == (4|5))) ||
				((.allowbind & 2) && (getiteminfo(@inventorylist_id[.@i],2) == (0|2|11|18))) ||
				((.allowbind & 4) && (getiteminfo(@inventorylist_id[.@i],2) == (3|6|7|8|10)))
			) {
				set .@bindlist$, .@bindlist$ + ":" + getitemname(@inventorylist_id[.@i]) + " - " + @inventorylist_id[.@i];
				set .@bindlist[.@j],.@i;
				.@j++;
			}
		}
		.@item = .@bindlist[select(.@bindlist$)-2];
		next;
		mes "Antes de continuar, eu quero que você saiba que não posso dizer a diferença entre vários itens iguais. Se você tem um item específico que você deseja vincular, remova todos os repetidos do inventário.";
		if(select("Eu entendo, continue:Espere um minuto") == 2) {
			next;
			mes "Eu estarei aqui até que você esteja pronto.";
			close;
		}
		next;
		mes "Tem certeza de que gostaria de vincular "+ getitemname(@inventorylist_id[.@item]) +" para o seu "+.boundtypes$[.@boundtype]+"?";
		if(select("Sim:Não") == 1) {
			Zeny -= .bindprice;
			delitem2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item];
			getitembound2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item],.@boundtype;
			mes "Tudo feito!";
			if(.logbinds)
				logmes "Vinculou "+ @inventorylist_amount[.@item]+" "+@inventorylist_id[.@item]+" as "+.boundtypes$[.@boundtype]+" type.";
		}
	} else {
		if(!countbound()) {
			mes "Você não tem nenhum item vinculado em seu inventário. Não há muito que eu possa fazer aqui.";
			close;
		}
		countbound(2);
		if(.unbindprice) {
			mes "Desvinculação de um item tem uma taxa de ^0000FF"+.unbindprice+"^000000 zeny.";
			if(Zeny < .unbindprice) {
				mes "Você não tem o suficiente para desvincular um item.";
				close;
			}
		}
		getinventorylist();
		for(.@i = 0; .@i < @inventorylist_count; .@i++) {
			if(@inventorylist_bound[.@i]) {
				set .@bindlist$, .@bindlist$ + ":" + getitemname(@inventorylist_id[.@i]) + " - " + @inventorylist_id[.@i];
				set .@bindlist[.@j],.@i;
				.@j++;
			}
		}
		.@item = .@bindlist[select(.@bindlist$)-2];
		next;
		for(.@i = 0; .@i < getarraysize(@bound_items); .@i++) {
			if(@inventorylist_id[.@item] == @bound_items[.@i] &&
				(!getcharid(2) || getguildmaster(getcharid(2)) != strcharinfo(0))
			) {
				mes "Atenção, Os itens vinculados de clãs só podem ser desvinculados pelo lider do clã.";
				close;
			}
		}
		mes "Antes de continuar, eu quero que você saiba que não posso dizer a diferença entre vários itens iguais. Se você tem um item específico que você deseja desvincular, remova todos os repetidos do inventário.";
		if(select("Eu entendo, continue:Espere um minuto") == 2) {
			next;
			mes "Eu estarei aqui até que você esteja pronto.";
			close;
		}
		next;
		mes "Tem certeza de que gostaria de desvincular "+ getitemname(@inventorylist_id[.@item]) +"?";
		if(select("Sim:Não") == 1) {
			Zeny -= .unbindprice;
			delitem2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item];
			getitem2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item];
			mes "Tudo feito!";
			if(.logbinds)
				logmes "Desvinculou "+ @inventorylist_amount[.@item]+" "+@inventorylist_id[.@item]+".";
		}
	}
	close;

OnInit:
	//* Configuration *\\
	//Price
	.bindprice = 0;
	.unbindprice = 100000;

	//What to allow to be bound - Add as necessary
	//1 = Equipment - 2 = Consumables - 4 = Etc
	.allowbind = 1;

	//Log binds via NPC?
	.logbinds = 1;

	//Other stuff
	.boundtypes$[1] = "account";
	.boundtypes$[2] = "guild";
	.boundtypes$[4] = "character";
	end;
}
