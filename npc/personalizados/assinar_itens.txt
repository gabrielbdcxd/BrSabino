//===== Hercules Script ======================================
//= Sign Your Items
//===== By: ==================================================
//= Lupus
//===== Compatível Com: ======================================
//= BRathena -- Tradução por: Julinhobrow
//===== Current Version: =====================================
//= 1.1a
//===== Description: =========================================
//= Escreva seu nome em equipamentos e armas raras ^_-
//===== Additional Comments: =================================
//= 1.1 Cleaned and edited for general use. [Euphy]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================

prt_in,24,61,7	script	Perchik	1_M_01,{
	
	setarray .@item[0],644,3;	// Itens Requeridos: <ID>,<Count>{,...} (0 para desativar)
	setarray .@cost[0],0,5000;	// Zeny Requerido: <base price>,<price per refine>

	disable_items;
	mes "[Perchik]";
	mes "Eu posso ^0055FFassinar seu nome^000000 na maioria dos itens raros que você segurar.";
	next;
	if(select("Conte-me mais...:Assine meus itens, por favor!") == 1) {
		mes "[Perchik]";
		mes "Eu posso colocar seu nome em alguns equipamentos ou armas sem slot.";
		emotion e_ic;
		next;
		mes "[Perchik]";
		if (getarraysize(.@item) || getarraysize(.@cost)) {
			mes "Para meu trabalho eu aceito:";
			if (getarraysize(.@item)) for(set .@i,0; .@i<getarraysize(.@item); set .@i,.@i+2)
				mes " ~ "+.@item[.@i+1]+"x "+getitemname(.@item[.@i]);
			if (.@cost[0]) mes " ~ "+.@cost[0]+" Zeny";
			if (.@cost[1]) mes " ~ "+.@cost[1]+" Zeny por refine";
		}
		else mes "Eu trabalharia grátis, mas...";
		next;
		emotion e_cry;
		mes "[Perchik]";
		mes "Assim, Eu tenho 12 crianças famintas";
		mes "e uma esposa muito braba.";
		next;
		mes "[Perchik]";
		mes "Ou é 12 crianças raivosas";
		mes "e uma esposa muito faminta...";
		emotion e_hmm;
		close;
	}
	mes "[Perchik]";
	mes "Mostre-me o item para assinar...";
	next;
	setarray .@pos$[1],"Upper Headgear","Armor","Left hand","Right hand","Garment","Footgear","Left Accessory","Right Accessory","Mid Headgear","Lower Headgear";
	set .@menu$,"";
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@i))
			set .@menu$, .@menu$+.@pos$[.@i]+" [^0055FF"+getequipname(.@i)+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, select(.@menu$);
	set .@ID, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[Perchik]";
	if (!getequipisequiped(.@part)) {
		mes "Nada está equipado aqui!";
		emotion e_wah;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "Veja, este item ja está assinado.";
		mes "Eu nunca devo tocar num trabalho de mestre.";
		emotion e_hmm;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "Uma carta? Aqui?!";
		mes "Como eu disse antes, não posso assinar itens com cartas.";
		emotion e_hmm;
		close;
	}
	if (getiteminfo(.@ID,10)) {
		mes "Desculpe, não assino itens slotados!";
		emotion e_sry;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "Desculpe, não assino itens alugados!";
			emotion e_hmm;
			close;
		}
	set .@price, .@cost[0]+(.@cost[1]*.@ref);
	if (getarraysize(.@item) || .@price) {
		mes "Posso precisar de:";
		if (getarraysize(.@item)) for(set .@i,0; .@i<getarraysize(.@item); set .@i,.@i+2) {
			mes " ~ "+.@item[.@i+1]+"x "+getitemname(.@item[.@i]);
			if (countitem(.@item[.@i]) < .@item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes " ~ "+.@price+" Zeny";
	}
	mes "Posso assinar o item ^0055FF"+getitemname(.@ID)+"^000000?";
	next;
	if(select("Ok!:Deixar")==2) {
		mes "[Perchik]";
		mes "Ti vejo depois...";
		emotion e_yawn;
		close;
	}
	mes "[Perchik]";
	if (Zeny < .@price || .@nr) {
		mes "Não trabalho por 'Obrigados'.";
		emotion e_sry;
		close;
	}
	if (!countitem2(.@ID,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "Onde está "+getitemname(@ID)+"...?";
		npctalk "Você é um pequeno golpista!";
		logmes "GOLPISTA: Pediu pra assinar o item e não tem: "+getitemname(@ID);
		emotion e_wah;
		close;
	}
	if (.@price) Zeny -= .@price;
	if (getarraysize(.@item))  for(set .@i,0; .@i<getarraysize(.@item); set .@i,.@i+2)
		delitem .@item[.@i], .@item[.@i+1];
	delitem2 .@ID,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "Feito!";
	emotion e_proud;
	getitem2 .@ID,1,1,.@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	equip .@ID;
	close;
}
