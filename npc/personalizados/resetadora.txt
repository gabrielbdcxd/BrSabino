//===== Hercules Script ======================================
//= Reset NPC
//===== By: ==================================================
//= Hercules Dev Team
//===== Current Version: =====================================
//= 1.3
//===== Compatível Com: ==========================================
//= BRathena -- Tradução corrigida por: Julinhobrow
//===== Descrição: ===============================================
//= Reseta pontos de Habilidades e Atributos.
//===== Comentários Adicionais: ==================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//============================================================

prontera,150,193,6	script	Garota Resetadora	4_F_TELEPORTER,{

	set .@ResetStat,5000;	// Zeny for stat reset
	set .@ResetSkill,5000;	// Zeny for skill reset
	set .@ResetBoth,9000;	// Zeny for resetting both together

	mes "[Resetadora]";
	mes "Olá, "+strcharinfo(0)+", eu sou a Resetadora.";
	mes "Resetar Atributos: "+ .@ResetStat +"z";
	mes "Resetar Habilidades: "+ .@ResetSkill +"z";
	mes "Resetar Ambos: "+ .@ResetBoth +"z";
	mes "Por favor selecione o serviço desejado:";
	next;
	switch(select("^FF3355Resetar Habilidades:Resetar Atributos:Resetar Ambos^000000:Sair")) {
	case 1:
		mes "[Resetadora]";
		if (Zeny < .@ResetSkill) {
			mes "Desculpe-me, mas você não possui zeny suficiente.";
			close;
		}
		Zeny -= .@ResetSkill;
		sc_end SC_ALL;
		resetskill;
		mes "Pronto, seus pontos de habilidades foram resetados.";
		close;
	case 2:
		mes "[Resetadora]";
		if (Zeny < .@ResetStat) {
			mes "Desculpe-me, mas você não possui zeny suficiente.";
			close;
		}
		Zeny -= .@ResetStat;
		resetstatus;
		mes "Pronto, seus pontos de atributos foram resetados.";
		close;
	case 3:
		mes "[Resetadora]";
		if (Zeny < .@ResetBoth) {
			mes "Desculpe-me, mas você não possui zeny suficiente.";
			close;
		}
		Zeny -= .@ResetBoth;
		sc_end SC_ALL;
		resetskill;
		resetstatus;
		mes "Pronto, todos os seus pontos foram resetados.";
		close;
	case 4:
		close;
	}
}
