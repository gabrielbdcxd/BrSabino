//===== Hercules Script ======================================
//= Valentine's Day Event (2012)
//===== Por: ==================================================
//= Rikimaru
//= Linne Ribeiro
//===== Versão Atual: =====================================
//= 1.2
//===== Descrição: =========================================
//= The full Valentine's Day NPCs of the Valentine's Day
//= Event 2012 (account-based).
//===== Comentários Adicionais: =================================
//= 1.0 First version. [Rikimaru]
//= 1.1 Standardized and fixed. [Euphy]
//= 1.2 Traduzido para Português. [Linne Ribeiro]
//============================================================

mosk_in,21,246,5	script	Pinkamenia	1_F_LIBRARYGIRL,{
	.@n$ = "^0000FF[Pinkamenia]^000000";
	if (#V_QUE12==3) {
		.@LFlowerID = 7864;
		setarray .@SweetsID[0],538,558,539,573,559,560,12062,596,597,12414,12319;
		setarray .@SweetsCost[0],1,2,5,10,10,10,15,15,15,20,20;
		mes .@n$;
		mes "Olá "+strcharinfo(0)+" !";
		mes "Você deseja comprar alguns itens?";
		mes "Mais antes, você terá de me dar amor";
		mes "Traga-me Flores do Amor, é claro!";
		next;
		if(select("Sim, com certeza!","Não, não importa.") == 2) close;
		next;
		mes .@n$;
		mes "Selecione um item.";
		mes "A quantidade de Flores do Amor que você precisa está entre parênteses.";
		for(.@i = 0; .@i < getarraysize(.@SweetsID); ++.@i)
			.@menu$ += "^00AA00["+.@SweetsCost[.@i]+"]^000000 "+getitemname(.@SweetsID[.@i])+":";
		next;
		.@choice = select(.@menu$)-1;
		mes .@n$;
		if (countitem(.@LFlowerID) < .@SweetsCost[.@choice]) {
			mes "Você vai precisar de mais Flores do amor se você quer isso!";
			close;
		}
		mes "Você tem certeza que quer trocar ^00aa00"+.@SweetsCost[.@choice]+"x Flor do Amor ^000000 por ^0055FF"+getitemname(.@SweetsID[.@choice])+"^000000?";
		if(select("Não, eu mudei de idéia.:Sim, Trade!") == 1) close;
		.@new = .@LFlowerID - .@SweetsCost[.@choice];
		if (.@new < 0) .@new = 0;
		delitem .@LFlowerID, .@SweetsCost[.@choice];
		getitem .@SweetsID[.@choice],1;
		mes "Divirta-se com o seu ITEM!";
		close;
	} else if (#V_QUE12==2) {
		mes .@n$;
		if (countitem(7864) < 15) {
			mes "Você tem que me trazer 15 Flores do amor!";
			close;
		}
		mes "Muito obrigado por";
		mes "Me tazer essas Flores do Amor.";
		delitem 7864,15;
		next;
		mes .@n$;
		mes "Leve isso como um pouco";
		mes "de meu Obrigado eterno.";
		getexp 500000,400000;
		getitem 617,1;
		getitem 12319,2;
		next;
		mes .@n$;
		mes "Se você receber mais amor";
		mes "Flores, você pode trocar";
		mes "Los para alguns doces aqui.";
		mes "Vejo você em breve!";
		#V_QUE12 = 3;
		close;
	} else if (#V_QUE12==1) {
		mes .@n$;
		mes "Por favor, traga um Cake Hat [+8]";
		mes "Ao Extraordinaire Baker";
		mes "Ao meu lado!";
		close;
	} else if (BaseLevel >= 45) {
		mes .@n$;
		mes "Olá "+strcharinfo(0)+", é";
		mes "Dia dos Namorados e nós";
		mes "Gostamos de entregar doce";
		mes "chocolates.";
		next;
		mes .@n$;
		mes "Nosso problema é que nós";
		mes "Precisamos de uma pessoa forte";
		mes "Como você, que poderia ajudar.";
		mes "Mas primeiro você terá";
		mes "Que me trazer um Cake Hat [+8]";
		mes "Para Baker Extraordinário, que";
		mes "Está de pé ao meu lado!";
		#V_QUE12 = 1;
		close;
	}
	mes .@n$;
	mes "Hello "+strcharinfo(0)+"!";
	close;

OnInit:
	// Unknown item drop rate (currently set at 5%)
	setitemscript 5105,"{ bonus bDex,1; bonus bMaxSP,80; bonus3 bAddMonsterDropItem,7864,RC_DemiHuman,500; }",0;
	end;
}

mosk_in,21,244,3	script	Baker Extraordinário	4_M_NOVELIST,{
	.@n$ = "^0000FF[Baker Extraordinário]^000000";
	if(#V_QUE12==3) {
		mes .@n$;
		mes "Troque suas Flores do Amor com Pinkamenia!";
		close;
	} else if(#V_QUE12==2) {
		mes .@n$;
		mes "Traga 15 Flores do Amor para Pinkamenia!";
		close;
	} else if(#V_QUE12==1) {
		mes .@n$;
		if (getequipid(1) == 5024 && getequiprefinerycnt(1) >= 8) {
			mes "Ah, então Pinkamenia disse";
			mes "Para me trazer o Chapéu de Bolo [+8]";
			mes "Agora eu vou";
			mes "Trocar seu Chapéu de Bolo";
			mes "Para outro Chapéu!";
			next;
			mes .@n$;
			delitem 5024,1;
			getitem 5105,1;
			equip 5105;
			mes "Agora, se você usar o";
			mes "novo Chapéu de Bolo, há";
			mes "Uma chance de que";
			mes "Monstros Demi-humanos";
			mes "Dropem Flores do Amor!";
			next;
			mes .@n$;
			mes "Os mobs roubaram nossas Flores,";
			mes "que é preciso para criar";
			mes "nossos chocolates. agora saia.";
			mes "Drop 15 Flores do Amor";
			mes "E Leve até Pinkamenia!";
			#V_QUE12 = 2;
			close;
		} else {
			mes "Desculpe, mas onde está o seu";
			mes "Chapéu de Bolo [+8] ?";
			mes "Lembre-se";
			mes "Precisa te-lo equipado.!";
			close;
		}
	} else {
		mes .@n$;
		mes "Olá, "+strcharinfo(0)+"!";
		close;
	}
}
