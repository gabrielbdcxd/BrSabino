//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Quest do Mago                          |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.9/1.0/1.4                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Mago.                |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy e Protimus]                   |
//   | 1.2 Correção em algumas concatenações de funções [Dani]|
//   | - strcharinfo(), sex() e getitemname()                 |
//   | - Correções ortográficas                               |
//   | 1.3 Otimização [Holy & Hold]                           |
//   | 1.4 Formatação nos Diálogos [lututuiBR & Holy]         |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

geffen_in,164,124,4	script	Maga Guardiã	2_F_MAGICMASTER,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Maga Guardiã]";
				mes "Olá, faz bastante tempo que não nos vemos!";
				mes "Mas você não deveria estar morto?";
				next;
				mes "[Maga Guardiã]";
				mes "Ah, você deve ter sido renascido.";
				mes "Bem, eu estou contente em vê-lo novamente.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "Sinto muito, mas eu acho que você ainda não está pronto para se tornar um Mago.";
					mes "Por que você não termina de aprender as habilidades básicas primeiro?";
					next;
					mes "[Maga Guardiã]";
					mes "Fique à vontade.";
					mes "Quanto mais você aprende, mais você estará pronto para aprender magia.";
					close;
				}
				mes "[Maga Guardiã]";
				mes "Bem, já que que você passou no teste de Mago uma vez, eu não vou questionar a sua qualificação.";
				mes "Você quer ter suas habilidades mágicas de volta imediatamente, não é?";
				next;
				skill 143,0,0;
				jobchange Job_Mage_High;
				skill 157,1,0;
				mes "[Maga Guardiã]";
				mes "Uau, por alguma razão você está bem melhor do que antes.";
				mes "De qualquer forma, eu acredito que você irá fazer um trabalho melhor sendo um Arquimago.";
				close;
			}
			else {
				mes "[Maga Guardiã]";
			mes "Há algo mais que eu possa ajudá-lo?";
			mes "Se não, por que você não vai testar suas habilidades?";
			mes "O mundo está esperando por você~!";
			close;
			}
		}
		else {
		mes "O que, você está interessado na Guilda dos Magos?";
		mes "Eu não queria dizer isso, mas você não pertence a ela.";
		next;
		mes "[Maga Guardiã]";
		mes "Eu não sei por que você ainda está na minha frente, mas eu posso dizer que você não está destinado a ser um Mago.";
		close;
		}
	}
	mes "[Maga Guardiã]";
	mes "Hum?";
	next;
	switch(select("Quero ser Mago!:O que é preciso para ser um Mago?:Nada.")) {
	case 1:
		mes "[Maga Guardiã]";
		if (BaseJob == Job_Mage) {
			mes "Ei, o que faz aqui?";
			mes "Você já é um Mago, bobo!";
			close;
		}
		if (BaseJob != Job_Novice) {
			mes "Ei~ Vamos. Pare de bricadeiras.";
			mes "Você não pode ser um Mago porque você já possui outra classe.";
			close;
		}
		if (job_magician_q == 0) {
			mes "Você, "+((Sex)?"é mesmo uma gracinha. Definitivamente, meu tipo...":"até que você é bonitinha, deve fazer um grande sucesso...");
			next;
			mes "[Maga Guardiã]";
			mes "Aham... Como eu ia dizendo, você quer ser Mago?";
			mes "Então, preencha a ficha de inscrição, por favor.";
			next;
			if (select("Se inscrever para o teste.:Desistir.") == 1) {
				mes "[Maga Guardiã]";
				mes "Certo. Assine aqui mesmo.";
				mes "Oh, você é muito bom para escrever.";
				mes "Certo. seu nome é... "+strcharinfo(0)+".";
				next;
				mes "[Maga Guardiã]";
				mes "Agora é a hora que vou te dar o teste.";
				switch(rand(3)) {
				case 1:
					mes "Me faça uma ^3355FFMistura de Solução Numero. 1^000000";
					mes "e a traga pra mim.";
					job_magician_q = 1;
					break;
				case 2:
					mes "Me faça uma ^3355FFMistura de Solução Numero. 2^000000";
					mes "e a traga pra mim.";
					job_magician_q = 2;
					break;
				case 3:
					mes "Me faça uma ^3355FFMistura de Solução Numero. 3^000000";
					mes "e a traga pra mim.";
					job_magician_q = 3;
					break;
				default:
					mes "Me faça uma ^3355FFMistura de Solução Numero. 4^000000";
					mes "e a traga pra mim.";
					job_magician_q = 4;
				}
				next;
				getitem 1092,1; //Empty_Cylinder
				mes "[Maga Guardiã]";
				mes "Você pode achar os ingredientes nos livros da guilda.";
				mes "Então acho melhor dar uma olhada antes de ir embora.";
				next;
				mes "[Maga Guardiã]";
				mes "Assim que coletar todos os itens, use na máquina do centro para misturar eles e fazer a solução. Boa sorte!";
				close;
			}
			mes "[Maga Guardiã]";
			mes "O QUE~?! Mesmo após me dizer que queria ser um mago, você mudou de idéia?!";
			mes "Seja um pouco mais decisivo!";
			close;
		}
		mes "Certo? Pronto...?";
		if (getskilllv("NV_BASIC") < 9) {
			mes "Oh, mas que bobalhão.";
			mes "Não tem os requerimentos para fazer o teste.";
			next;
			mes "[Maga Guardiã]";
			mes "Volte e alcance level 9 de Classe primeiro.";
			mes "Não esqueça de que você também precisa aprender todas as Habilidades Básicas antes de voltar.";
			close;
		}
		mes strcharinfo(0)+" o que eu posso fazer por você?";
		if (job_magician_q == 1)
			mes "Prepare a Poção Mágica número 1 e traga aqui para mim.";
		else if (job_magician_q == 2)
			mes "Prepare a Poção Mágica número 2 e traga aqui para mim.";
		else if (job_magician_q == 3)
			mes "Prepare a Poção Mágica número 3 e traga aqui para mim.";
		else
			mes "Prepare a Poção Mágica número 4 e traga aqui para mim.";
		next;
		mes "[Maga Guardiã]";
		mes "Ok, agora vou verificar se está tudo certo.";
		next;
		mes "[Maga Guardiã]";
		if (countitem(1071) == 0 && countitem(1085) == 0 && countitem(1086) == 0 && countitem(1087) == 0 && countitem(1090) == 0) {
			mes "Onde está a Poção Mágica que eu pedi?";
			mes "Não posso saber se você fez certo se não me mostrar.";
			close;
		}
		else {
			if ((job_magician_q == 1 && countitem(1071) == 0) || (job_magician_q == 2 && countitem(1085) == 0) || (job_magician_q == 3 && countitem(1086) == 0) || (job_magician_q == 4 && countitem(1087) == 0)) {
				mes "... Não, não foi isso que eu pedi.";
				next;
				mes "[Maga Guardiã]";
				if (job_magician_q == 1)
					mes "Você deve fazer a Solução Mista N ° 1 e trazê-la de volta para mim. Agora vá e tente novamente.";
				else if (job_magician_q == 2)
					mes "Você deve fazer a Solução Mista N ° 2 e trazê-la de volta para mim. Agora vá e tente novamente.";
				else if (job_magician_q == 3)
					mes "Você deve fazer a Solução Mista N ° 3 e trazê-la de volta para mim. Agora vá e tente novamente.";
				else
					mes "Você deve fazer a Solução Mista N ° 4 e trazê-la de volta para mim. Agora vá e tente novamente.";

				if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
				else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
				else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
				else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
				else delitem 1090,1; //Mage_Test_Etc
				close;
			}
		}
		if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
		else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
		else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
		else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
		else delitem 1090,1; //Mage_Test_Etc
		mes "Hum... Eu vejo que você tentou bastante.";
		mes "Para um iniciante, está muito bom. Grande trabalho!";
		next;
		mes "[Maga Guardiã]";
		mes "Hum... Eu vejo que você tentou bastante.";
		mes "Para um iniciante, está muito bom. Grande trabalho!";
		next;
		mes "[Maga Guardiã]";
		mes "Isso ai!";
		mes "Eu tenho o prazer de lhe parabenizar e dizer que você passou no teste.!";
		next;
		mes "[Maga Guardiã]";
		mes "*Que bom*";
		mes "Parabéns! Você é um mago agora!";
		next;
		callfunc "Job_Change",Job_Mage;
		callfunc "F_ClearJobVar";
		getitem	1639,1; // Cajado de Iniciantes
		Zeny += 50;
		if (questprogress(1005)) {
			completequest 1005;
		}
		else if (questprogress(1006)) {
			completequest 1006;
		}
		else if (questprogress(1007)) {
			completequest 1007;
		}
		else {
			completequest 1008;
		}
		mes "[Maga Guardiã]";
		mes "'Bem vindo ao meu mundo~'";
		mes "Heh heh, eu sempre quis dizer isso.";
		mes "Você sabe, é como um filme já visto.";
		next;
		mes "[Maga Guardiã]";
		mes "Agora que você é um mago como nós, vamos ser amigos, ok?";
		close;
	case 2:
		mes "[Maga Guardiã]";
		mes "Quer ser um mago, huh?";
		if (Sex)
			mes "Para uma gracinha como você ficarei feliz em explicar!";
		else
			mes "Eu ficaria feliz de explicar para uma garotinha tão bonita como você!";
		next;
		mes "[Maga Guardiã]";
		mes "Em primeiro lugar, você tem que alcançar o Nível 10 de Aprendiz e aprender todas as Habilidades Básicas. Então, você terá que passar no Teste do Mago.";
		next;
		if (job_magician_q != 0) {
			mes "[Maga Guardiã]";
			mes "Your test is to";
			switch(job_magician_q) {
			case 1:
				mes "O seu teste é fazer uma";
				mes "^3355FFMistura de Solução No. 1^000000";
				mes "e trazer para mim.";
				setquest 1005;
				break;
			case 2:
				mes "O seu teste é fazer uma";
				mes "^3355FFMistura de Solução No. 2^000000";
				mes "and bring it back to me.";
				setquest 1006;
				break;
			case 3:
				mes "O seu teste é fazer uma";
				mes "^3355FFMistura de Solução No. 3^000000";
				mes "e trazer para mim.";
				setquest 1007;
				break;
			default:
				mes "O seu teste é fazer uma";
				mes "^3355FFMistura de Solução No. 4^000000";
				mes "e trazer para mim.";
				setquest 1008;
			}
			next;
			mes "[Maga Guardiã]";
			mes "Você pode olhar os ingredientes para a solução nos livros de magia aqui dentro da Guilda.";
		}
		else {
			mes "[Maga Guardiã]";
			mes "Você será informado sobre que solução deve fazer antes de ler os livros sobre elas.";
		}
		next;
		mes "[Maga Guardiã]";
		mes "Me diga quando estiver pronto para ser um mago, tudo bem?";
		close;
	case 3:
		mes "[Maga Guardiã]";
		mes "Nada...?";
		close;
	}
}

geffen_in,164,112,4	script	Máquina de Mistura	HIDDEN_NPC,{
	mes "[Máquina de Mistura]";
	mes	"Esta máquina é propriedade da Guilda dos Magos de Geffen e só pode ser utilizada para soluções e propósitos mágicos.";
	next;
	if (select("Usar a máquina.:Cancelar.") == 1) {
		mes "[Máquina de Mistura]";
		mes "Escolha o Solvente para a Solução.";
		next;
		switch(select("Solução de Payon.:Solução de Morroc.:Nenhum Solvente.")) {
		case 1:
			if (countitem(1089) == 0) {
				mes "[Máquina de Mistura]";
				mes "Erro. Item não encontrado.";
				mes "Por favor, verifique novamente.";
				mes "Processo cancelado.";
			close;
				close;
			}
			.@mixitem2 = 1;
			break;
		case 2:
			if (countitem(1088) == 0) {
				mes "[Máquina de Mistura]";
				mes "Erro. Item não encontado.";
				mes "Por favor, verifique novamente.";
				mes "Processo Cancelado.";
				close;
			}
			.@mixitem2 = 2;
			break;
		case 3:
			.@mixitem2 = 0;
			break;
		}
		while (1) {
			if (.@progress == 2) {
				mes "[Máquina de Mistura]";
				if (.@mixitem1_1 != 0) mes "Jellopy: " + .@mixitem1_1 + " ea.";
				if (.@mixitem1_2 != 0) mes "felpa: " + .@mixitem1_2 + " ea.";
				if (.@mixitem1_3 != 0) mes "Leite: " + .@mixitem1_3 + " ea.";
				if (.@mixitem2 == 0) mes "Solvent: nenhum.";
				if (.@mixitem2 == 1) mes "Solvent: solvente de payon.";
				if (.@mixitem2 == 2) mes "Solvent: solvente de morroc.";
				next;
				mes "[Máquina de Mistura]";
				mes "Por favor escolha o que você deseja começar a misturar, ou reponha os itens que deseja usar.";
				next;
				switch(select("Começar a misturar.:Repor o número de itens.:Resetar.")) {
				case 1:
					mes "[Máquina de Mistura]";
					mes "Por favor coloque os itens no recipiente.";
					mes "Tenha certeza que eles estão colocados na quantidade correta.";
					next;
					mes "[Máquina de Mistura]";
					mes "Você não pode adicionar ou restaurar os itens que forem colocados no recipiente.";
					next;
					mes "[Máquina de Mistura]";
					mes "Se está tudo correto, aperte o botão 'misturar'.";
					mes "Caso contrário, aperte 'cancelar'.";
					next;
				if (select("Misturar.:Cancelar.") == 2) break;
				mes "[Máquina de Mistura]";
				mes "Colocando os itens no recipiente. Por favor, aguarde.";
				next;
					if (select("Precione 'Misturar' Botão.:Precione 'Cancelar' Botão.") == 1) {
						mes "[Máquina de Mistura]";
						mes "Coloque os itens no recipiente de mistura agora.";
						next;
						mes "[Máquina de Mistura]";
						if (countitem(909) < .@mixitem1_1) {
							mes "Jellopy insuficiente.";
							mes "Por favor, verifique novamente.";
							mes "Processo interrompido.";
							close;
						}
						else if (countitem(914) < .@mixitem1_2) {
							mes "Felpa insuficiente.";
							mes "Por favor, verifique novamente.";
							mes "Processo interrompido.";
							close;
						}
						else if (countitem(519) < .@mixitem1_3) {
							mes "Leite insuficiente.";
							mes "Por favor, verifique novamente.";
							mes "Processo interrompido.";
							close;
						}
						else if (.@mixitem2 == 1 || .@mixitem2 == 2) {
							if (countitem(1089) == 0 && countitem(1088) == 0) {
								mes "Solução não encontrada.";
								mes "Por favor, verifique novamente.";
								mes "Processo interrompido.";
								close;
							}
						}
						if (.@mixitem1_1 != 0) delitem 909,.@mixitem1_1; //Jellopy
						if (.@mixitem1_2 != 0) delitem 914,.@mixitem1_2; //Felpa
						if (.@mixitem1_3 != 0) delitem 519,.@mixitem1_3; //Leite
						if (.@mixitem2 == 1) delitem 1089,1; //Payon_Potion
						if (.@mixitem2 == 2) delitem 1088,1; //Morocc_Potion
							mes "Itens estão prontos. Fechando a porta.";
						.@progress = 3;
						next;
					}
					break;
				case 2:
					.@continue = 0;
					next;
					break;
				case 3:
					.@mixitem1_1 = 0;
					.@mixitem1_2 = 0;
					.@mixitem1_3 = 0;
					.@progress = 0;
					.@continue = 0;
					mes "[Máquina de Mistura]";
					mes "Processo resetado.";
					mes "Começar novamente?";
					next;
					if (select("Sim.:Não.") == 1)
						break;
					mes "[Máquina de Mistura]";
					mes "Processo interrompido. Obrigado.";
					close;
				}
				if (.@progress == 3) break;
			}
			else if (.@progress == 1) {
				mes "[Máquina de Mistura]";
				mes "Nada encontrado.";
				next;
			}
			mes "[Máquina de Mistura]";
			mes "Selecione os itens para a mistura.";
			while(1) {
				switch(select("Jellopy.:Felpa.:Leite.:Pronto para misturar.")) {
				case 1:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Máquina de Mistura]";
							mes "Erro: O limite de itens ultrapassou.";
							mes "Por favor coloque valores menores que 10,000 e tente novamente.";
							next;
						}
						else {
							if (countitem(909) > 0) .@mixitem1_1 += .@input;
							.@progress = 2;
							break;
						}
					}
					break;
				case 2:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Máquina de Mistura]";
							mes "Erro: O limite de itens ultrapassou.";
							mes "Por favor coloque valores menores que 10,000 e tente novamente.";
							next;
						}
						else {
							if (countitem(914) > 0) .@mixitem1_2 += .@input;
							.@progress = 2;
							break;
						}
					}
					break;
				case 3:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Máquina de Mistura]";
							mes "Erro: O limite de itens ultrapassou.";
							mes "Por favor coloque valores menores que 10,000 e tente novamente.";
							next;
						}
						else {
						if (countitem(519) > 0) .@mixitem1_3 += .@input;
							.@progress = 2;
							break;
						}
					}
					break;

				case 4:
					if (.@progress != 2)
						.@progress = 1;
					.@continue = 1;
					next;
				}
				if (.@continue) break;
			}
		}
		mes "[Máquina de Mistura]";
		mes "Por favor digite o número de série do Pó Mágico.";
		next;
		while(1) {
			input .@input;
			if (.@input < 1000 || .@input > 9999) {
				mes "[Máquina de Mistura]";
				if (.@input == 0) {
					mes "Deseja pular este menu?";
					next;
					if (select("Sim.:Não.") == 1) {
						break;
					}
				}
				else {
					mes "Número inválido. Por favor, tente novamente.";
					next;
				}
			}
			else {
				mes "[Máquina de Mistura]";
				mes "O numero de série é #" + .@input + ", correto?";
				next;
				if (select("Confirmar.:Cancelar.") == 1) {
					if (.@input == 8472)
						.@magic_powder = 1;
					else if (.@input == 3735)
						.@magic_powder = 2;
					else if (.@input == 2750)
						.@magic_powder = 3;
					else if (.@input == 5429)
						.@magic_powder = 4;
					else
						.@magic_powder = 5;
				}
				break;
			}
		}
		mes "[Máquina de Mistura]";
		mes "Escolha uma Pedra Catalisadora.";
		next;
		switch(select("Gema Amarela.:Gema vermelha.:Gama Azul.:Diamente de 1 Quilate.:Pular a etapa.")) {
		case 1: .@mixitem3 = 1; break;
		case 2: .@mixitem3 = 2; break;
		case 3: .@mixitem3 = 3; break;
		case 4: .@mixitem3 = 4; break;
		case 5: break;
		}
		mes "[Máquina de Mistura]";
		mes "Tudo pronto. Iniciando. Processo de Mistura. Por favor, aguarde.";
		next;
		mes "[Máquina de Mistura]";
		mes "- Proverb of the Day -";
		switch(rand(1,5)) {
		case 1: mes "Olho por olho: Quando tira de uma pessoa, você deve retribuí-lo de alguma forma.";	break;
		case 2:	mes "O valor da confiança não pode ser medido ou muito menos comprado."; break;
		case 3: mes "Não tratar os outros da maneira que você não gostaria que te tratariam. Ou seja, quando você machuca alguém você será ferido mais ainda."; break;
		case 4:	mes "Um bom vizinho é melhor do que um irmão distante: quando precisar de ajuda, conte sempre com quem esta perto de você."; break;
		default: mes "Rebanho de aves plumadas: Você pode definir o caráter de alguém através dos seus amigos.";
		}
		next;
		if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 1 && .@mixitem2 == 1 && .@mixitem3 == 1 && .@magic_powder == 1) {
			mes "[Máquina de Mistura]";
			mes "Teste do Mago Solução Nº1";
			getitem 1071,1; //Mage_Test_1
			next;
		}
		else if (.@mixitem1_1 == 3 && .@mixitem1_2 == 1 && .@mixitem1_3 == 1 && .@mixitem2 == 0 && .@mixitem3 == 2 && .@magic_powder == 2) {
			mes "[Máquina de Mistura]";
			mes "Teste do Mago Solução Nº2";
			getitem 1085,1; //Mage_Test_2
			next;
		}
		else if (.@mixitem1_1 == 6 && .@mixitem1_2 == 1 && .@mixitem1_3 == 0 && .@mixitem2 == 1 && .@mixitem3 == 3 && .@magic_powder == 3) {
			mes "[Máquina de Mistura]";
			mes "Teste do Mago Solução Nº3";
			getitem 1086,1; //Mage_Test_3
			next;
		}
		else if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 0 && .@mixitem2 == 2 && .@mixitem3 == 4 && .@magic_powder == 4) {
			mes "[Máquina de Mistura]";
			mes "Teste do Mago Solução Nº4";
			getitem 1087,1; //Mage_Test_4
			next;
		}
		else {
			mes "[Máquina de Mistura]";
			mes "Erro inesperado ocorrido.";
			getitem 1090,1; //Mage_Test_Etc
			next;
		}
		mes "[Máquina de Mistura]";
		mes "Mistura completa. Obrigado.";
		close;
	}
	close;
}

// The Morroc Solution and Payon Solution traders
// are also used in the Sage skill quests, and have
// been moved to npc/quests/mage_solution.txt.
//
// pay_arche,122,100,0	script	Dollshoi	4_M_ORIENT01,{}
// moc_ruins,91,150,0	script	Ponka-Hontas	4_F_04,{}

geffen_in,177,112,4	script	Estante de Livros	HIDDEN_NPC,{
	mes "[Livro Guia]";
	mes "Este guia é propriedade da Guilda dos Magos de Geffen.";
	next;
	switch(select("Solução No. 1.:Solução No. 2.:Solução No. 3.:Solução No. 4.:Sair.")) {
	case 1:
		mes "[Teste do Mago Solução Nº1]";
		mes "* Lista de ingredientes *";
		mes "2 Jellopy";
		mes "3 Felpa";
		mes "1 Leite";
		next;
		mes "[Teste do Mago Solução Nº1]";
		mes "* Solvente *";
		mes "Solução de Payon";
		mes "Onde encontrá-la: Perto de uma lagoa na vila dos arqueiros.";
		next;
		mes "[Teste do Mago Solução Nº1]";
		mes "*Código de série do pó mágico*";
		mes "8472";
		next;
		mes "[Teste do Mago Solução Nº1]";
		mes "* Catalisador * ";
		mes "Gema Amarela";
		mes "(Provida pela Máquina de mistura)";
		close;
	case 2:
		mes "[Teste do Mago Solução Nº2]";
		mes "* Lista de ingredientes *";
		mes "3 Jellopy";
		mes "1 Felpa";
		mes "1 Leite";
		next;
		mes "[Teste do Mago Solução Nº2]";
		mes "* Solvente *";
		mes "Nenhum";
		next;
		mes "[Teste do Mago Solução Nº2]";
		mes "* Código de série do pó mágico *";
		mes "3735";
		next;
		mes "[Teste do Mago Solução Nº2]";
		mes "* Catalisador *";
		mes "Gema Vermelha";
		mes "(Provida pela Máquina de mistura)";
		close;
	case 3:
		mes "[Teste do Mago Solução Nº3]";
		mes "* Lista de ingredientes *";
		mes "6 Jellopy";
		mes "1 Felpa";
		next;
		mes "[Teste do Mago Solução Nº3]";
		mes "* Solvente *";
		mes "Solução de Payon";
		mes "Onde encontrá-la: Perto de uma lagoa na vila dos arqueiros..";
		next;
		mes "[Teste do Mago Solução Nº3]";
		mes "* Código de série do pó mágico *";
		mes "2750";
		next;
		mes "[Teste do Mago Solução Nº3]";
		mes "* Catalisador *";
		mes "Gema Azul";
		mes "(Provida pela Máquina de mistura)";
		close;
	case 4:
		mes "[Teste do Mago Solução Nº4]";
		mes "* Lista de ingredientes *";
		mes "2 Jellopy";
		mes "3 Felpa";
		next;
		mes "[Teste do Mago Solução Nº4]";
		mes "* Solvente *";
		mes "Solução de Morroc";
		mes "Aonde encontrar: Em uma lagoa perto da Pirâmide de Morroc.";
		next;
		mes "[Teste do Mago Solução Nº4]";
		mes "* Código de série do pó mágico *";
		mes "5429";
		next;
		mes "[Teste do Mago Solução Nº4]";
		mes "* Catalisador *";
		mes "Diamente de 1 Quilate";
		mes "(Provided by";
		mes "(Provida pela Máquina de mistura)";
		close;
	}
	close;
}
