//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Quest do Arqueiro                      |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.9/1.2/1.3                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Arqueiro.            |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy e Protimus]                   |
//   | 1.2 Otimização [Holy & Hold, lututuiBR]                |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

payon_in02,64,71,4	script	Guia dos Arqueiros#arqueiro	4_M_03,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy)) {
			mes "[Guia dos Arqueiros]";
			mes "Ei, eu te conheço.";
			mes "Você já fez esse teste antes, não foi?";
			next;
			mes "[Guia dos Arqueiros]";
			mes "Ah, você deve ter ido a Valhalla renascer.";
			mes "Uau, é tão impressionante!";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Guia dos Arqueiros]";
				mes "Err...";
				mes "É melhor aprender todas as habilidades básicas antes de se tornar um Arqueiro.";
				next;
				mes "[Guia dos Arqueiros]";
				mes "Certo, até logo.";
				close;
			}
			mes "[Guia dos Arqueiros]";
			mes "Bem então. Eu não preciso dizer mais nada.";
			mes "Eu sei que você pode se tornar um grande Arqueiro...";
			next;
			skill 143,0,0;
			jobchange Job_Archer_High;
			skill 147,1,0;
			skill 148,1,0;
			mes "[Guia dos Arqueiros]";
			mes "Embora não haja uma premiação especial por enquanto, eu espero que você entenda.";
			mes "Tenha cuidado.";
			close;
		}
		else {
			mes "[Guia dos Arqueiros]";
			mes "Oh...?";
			mes "Ei, o que você está fazendo aqui...?";
			next;
			mes "[Guia dos Arqueiros]";
			mes "Eu posso dizer que você não está destinado para ser um Arqueiro.";
			mes "Algo me da a entender que você deve procurar fazer uma outra coisa...";
			close;
		}
	}
	mes "[Guia dos Arqueiros]";
	mes "Prazer em conhecê-lo. Como posso ajudá-lo?";
	next;
	switch(select("Eu quero me tornar um Arqueiro.:Eu preciso saber dos requerimentos, por favor.:Nada, obrigado.")) {
	case 1:
		if (BaseJob == Job_Archer) {
			mes "[Guia dos Arqueiros]";
			mes "Você já é um Arqueiro...";
			close;
		}
		else if (BaseJob != Job_Archer && BaseJob != Job_Novice) {
			mes "[Guia dos Arqueiros]";
			mes "Hmm...";
			mes "Você não é um aprendiz";
			next;
			mes "[Guia dos Arqueiros]";
			mes "Enfim, o que você é? você não pode escolher ser um Arqueiro porque você tem uma Classe.";
			close;
		}
		if (job_archer_q == 0) {
			mes "[Guia dos Arqueiros]";
			mes "Você quer ser um arqueiro?";
			mes "Se você quer, preencha este formulário.";
			next;
			if (select("Inscrever-se.:Cancelar") == 1) {
				job_archer_q = 1;
				setquest 1004;
				mes "[Guia dos Arqueiros]";
				mes "Ok, assine aqui...";
				mes "Certo...";
				mes "Hum, você será promovido de classe assim que cumprir os pré-requisitos necessários.";
				next;
				mes "[Guia dos Arqueiros]";
				mes "Se você acha que está pronto, podemos começar agora. Você está pronto?";
				next;
				if (select("Sim!:Não, não estou pronto ainda.") == 1) {
					mes "[Guia dos Arqueiros]";
					mes "Tudo bem, deixe-me verificar.";
					next;
				}
				else {
					mes "[Guia dos Arqueiros]";
					mes "Compreendo...";
					mes "Se você quiser saber os pré-requisitos para se tornar um Arqueiro, apenas fale comigo.";
					close;
				}
			}
			mes "[Guia dos Arqueiros]";
			mes "Vamos ver então.";
			close;
		}
		mes "[Guia dos Arqueiros]";
		mes "ocê se chama " + strcharinfo(0) + "?";
		next;
		if (getskilllv("NV_BASIC") < 9) {
			mes "O seu nível como Aprendiz está abaixo do necessário...";
			mes "Você deve chegar ao nível 10 de classe para se tornar um Arqueiro.";
			close;
		}
		if (job_archer_q == 1) {
			.@archer_item1 = countitem(1066) * 5;
			.@archer_item2 = countitem(1067) * 3;
			.@archer_item3 = countitem(1068) * 2;
			.@archer_item4 = countitem(1019);
			.@total_archer = .@archer_item1 + .@archer_item2 + .@archer_item3 + .@archer_item4;
			.@total_archer2 = (((.@archer_item2 + .@archer_item3) * 2) + .@archer_item4);
			mes "[Guia dos Arqueiros]";
			mes "Excelente!";
			mes "Agora então,";
			mes "vamos ver ...";
			next;
			mes "[Guia dos Arqueiros]";
			mes "Agora vamos ver se você trouxe Troncos suficientes para fazermos um Arco.";
			next;
			if (countitem(1066) == 0 && countitem(1067) == 0 && countitem(1068) == 0 && countitem(1019) == 0) {
				mes "[Guia dos Arqueiros]";
				mes "...Hum...";
				mes "Você não me trouxe nada!";
				close;
			}
			mes "[Guia dos Arqueiros]";
			if (countitem(1066) != 0) mes " Grade S : " + countitem(1066) + " ea, Grade: " + .@archer_item1 + " . ";
			if (countitem(1067) != 0) mes " Grade A : " + countitem(1067) + " ea, Grade : " + .@archer_item2 + " . ";
			if (countitem(1068) != 0) mes " Grade B : " + countitem(1068) + " ea, Grade : " + .@archer_item3 + " . ";
			if (countitem(1019) != 0) mes " Grade C : " + countitem(1019) + " ea, Grade : " + .@archer_item4 + " . ";
			if (.@total_archer < 25) {
				mes "O seu total de pontos foi: ^FF0000" + .@total_archer + "^000000 / 40";
				next;
				mes "[Guia dos Arqueiros]";
				mes "Menos de 25?!?! Se esforce um pouco mais...";
				close;
			}
			else {
				mes "O seu total de pontos foi: ^0000FF" + .@total_archer + "^000000 / 40";
				next;
				mes "[Guia dos Arqueiros]";
				if (.@total_archer > 40) {
					mes "Nossa! Mais de 40! Fantástico! Meus parabéns!";
				}
				else if (.@total_archer > 30) {
					mes "Mais de 30! Ótimo! Parabéns.";
				}
				else {
					mes "*Sigh*Bem, você apenas passou ... De qualquer forma, bom trabalho.";
				}
			}
			next;
			mes "[Guia dos Arqueiros]";
			mes "Vou mandar o material para o Departamento de Produção de Arcos.";
			if (countitem(1066) != 0) delitem 1066,countitem(1066); //Tree_Of_Archer_1
			if (countitem(1067) != 0) delitem 1067,countitem(1067); //Tree_Of_Archer_2
			if (countitem(1068) != 0) delitem 1068,countitem(1068); //Tree_Of_Archer_3
			if (countitem(1019) != 0) delitem 1019,countitem(1019); //Wooden_Block
		}
		next;
		callfunc "Job_Change",Job_Archer;
		callfunc "F_ClearJobVar";
		completequest 1004;
		mes "[Guia dos Arqueiros]";
		mes "Parabéns, você passou no teste!";
		mes "Vou nomeá-lo um Arqueiro!";
		next;
		mes "[Guia dos Arqueiros]";
		mes "Nós esperamos que você contribua para a grandiosidade de nossa Guilda!";
		next;
		mes "[Guia dos Arqueiros]";
		mes "Ah! Seu Arco chegou do Departamento de Produção, aqui está ele.";
		getitem 1702,1; //Bow_
		getitem 1750,.@total_archer2; //Arrow
		next;
		mes "[Guia dos Arqueiros]";
		mes "Agora você é um Arqueiro legítimo, com arco, flecha e tudo mais!";
		mes "Boa Sorte em sua jornada pelo mundo, espero que você se torne o melhor de todos os Arqueiros!";
		close;
	case 2:
		mes "[Guia dos Arqueiros]";
		mes "Vou explicar os requisitos para ser um arqueiro.";
		if (BaseJob != Job_Novice) {
			if (BaseJob == Job_Archer) {
				next;
				mes "[Guia dos Arqueiros]";
				mes "Mas... Você já é um Arqueiro.";
				mes "Já deve saber de tudo...";
			}
			else {
				next;
				mes "Espere um segundo.";
				mes "Você já escolheu uma classe diferente. Como não sabe disso~";
			}
			mes "Então... Bem...";
			mes "Não há nenhuma razão para eu te ensinar os requerimentos...";
		}
		next;
		mes "[Guia dos Arqueiros]";
		mes "Bem, você deve possuir o nível de habilidades exigido.";
		mes "Seu nível de classe deve ser de pelo menos ^4A4AFF10^000000 e seu nível de habilidades básicas deve ser de ^4A4AFFNível 9^000000";
		next;
		mes "[Guia dos Arqueiros]";
		mes "Um Arqueiro precisa de uma concentração extremamente elevada, por isso nós não aceitamos aqueles que têm pouca paciência.";
		mes "Você também deve recolher ^FF0000Troncos^000000.";
		mes "Existem 4 tipos diferentes de Troncos, cada um deles se difere na qualidade.";
		mes "Você irá receber notas diferentes para seus troncos, dependendo de sua qualidade.";
		next;
		mes "[Guia dos Arqueiros]";
		mes "Para se tornar um Arqueiro, você deve receber uma nota de pelo menos ^0000FF25^000000 pontos dos 40.";
		mes "Você pode conseguir Troncos derrotando 'Salgueiros'.";
		mes "Tenha cuidado eles podem ser monstros difíceis de se derrotar.";
		mes "Um arqueiro utiliza arco e possui várias habilidades e talentos.";
		mes "A maior habilidade de um arqueiro é poder atacar inimigos a distância.";
		next;
		mes "[Guia dos Arqueiros]";
		mes "Apesar do Arqueiro possuir a menor quantidade de HP, eles podem atacar seus inimigos de longo alcance, então devido a isso eles se tornam mais seguros em uma batalha.";
		next;
		mes "[Guia dos Arqueiros]";
		mes "Arqueiros também possuem alta precisão e taxa de ataque com arcos, eles podem derrotar um monstro sem nem mesmo serem tocados.";
		next;
		mes "[Guia dos Arqueiros]";
		mes "^8C2121Um Arqueiro pode avançar de classe e se tornar um Caçador.";
		mes "^000000 ^8C2121Além de Caçador, você poderá mudar sua classe para "+((Sex) ? "Bardo" : "Odalisca")+".^000000";
		close;
	case 3:
		mes "[Guia dos Arqueiros]";
		mes "Se você tiver alguma dúvida, sinta-se a vontade para perguntar.";
		close;
	}
}

