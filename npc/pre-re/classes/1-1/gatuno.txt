//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Quest do Gatuno                        |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.8/1.2/1.3                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Gatuno.              |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy e Protimus]                   |
//   | 1.2 Otimização [Holy & Hold]                           |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

moc_prydb1,39,129,2	script	Gatuna Ajudante	1_F_04,{
	if (Upper == 1) {
		if (ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) {
			if (Class == Job_Novice_High) {
				mes "[Gatuna Ajudante]";
				mes "Hã? Eu te conheço? É assustador que você parece tão familiar. Você não tem um irmão gêmeo, que pareça com você?";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Gatuna Ajudante]";
					mes "O que, você quer ser um gatuno? Sinto muito, mas parece que você precisa de mais treinamento.";
					next;
					mes "[Gatuna Ajudante]";
					mes "Gaste seu tempo e aprenda todas as habilidades básicas, certo? Bem, então vejo você mais tarde~!";
					close;
				}
				mes "[Gatuna Ajudante]";
				mes "Bem, eu tenho essa sensação de que você já passou por uma vida inteira de lutas, então eu estou promovendo-o a um Gatuno neste minuto. É melhor eu deixar caras Durões cuidando de você...";
				next;
				skill 143,0,0;
				jobchange Job_Thief_High;
				skill 149,1,0;
				skill 150,1,0;
				skill 151,1,0;
				skill 152,1,0;
				mes "[Gatuna Ajudante]";
				mes "Ao se tornar um gatuno, irá viver como um Gatuno. Agora, vá em frente! Próximo~";
				close;
			}
			else {
				mes "[Gatuna Ajudante]";
				if (Sex)
					mes "Ei, cara.";
				else
					mes "Ei, moça";
				close;
			}
		}
		else {
			mes "[Gatuna Ajudante]";
			if (Sex)
				mes "Ei, cara.";
			else
				mes "Ei, moça";
			mes "Você parece muito uma boa pessoa para querer ser um gatuno! Agora suma, estou ocupado. Próximo!";
			close;
		}
	}
	if (BaseJob == Job_Thief) {
		mes "[Gatuna Ajudante]";
		mes "Ei~ Se você tiver algum problema, fale quando quiser, tudo bem?";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[Gatuna Ajudante]";
		mes "What the heck...?";
		switch(Class) {
		case 1: mes "Mais um Espadachim metido..."; break;
		case 2: mes "Não precisamos de fogos de artíficio aqui... Vai acender fogueira sem isqueiro, vai...."; break;
		case 3: mes "Haha... apenas um arqueiro cegueta...."; break;
		case 4: mes "Ei noviço, ajoelha aí e reza um terço..."; break;
		case 5:	mes "Ahhh um mercador trambiqueiro!! Vou te denunciar pros GMs...";	break;
		case 8: mes "Por que mais um sacerdote veio aqui? Eu acho que é melhor eu começar confessando todos os meus pecados."; break;
		case 12: mes "Será que você não era um de nós?! Cara, você mudou. Você parece ser perigoso agora..."; break;
		case 17: mes "Você deve ter algumas habilidades que só posso imaginá-las!";	break;
		}
		next;
		mes "[Gatuna Ajudante]";
		mes "*Suspiro*";
		mes "Bem, aqui não é lugar para você. Você deveria sair daqui.";
		close;
	}
	if (job_thief_q == 3 && countitem(1069) > 0 || countitem(1070) > 0) {
		mes "Humm? Você coletou todos os Cogumelos do teste de Gatuno, certo?";
		next;
		mes "[Gatuna Ajudante]";
		mes "Fale com o outro homem ao meu lado. Ele é o encarregado de verificar seus Cogumelos.";
		close;
	}
	else if (job_thief_q == 3) {
		mes "Então, como foi na fazenda de cogumelos? Se divertiu bastante?";
		next;
		if (select("Yeah, kinda Cool.:It was horrible.") == 1) {
			mes "[Gatuna Ajudante]";
			mes "Caramba! Você é O cara mesmo!";
			mes "Todos os outros foram uns %#@%#%%@ comparados a você!";
			mes "Humm... Você não vai tentar me substituir, não é? Por que se você sequer tentar, eu vou enfiar uma faca em você tão rápido que você nem vai perceber...";
			close;
		}
		mes "[Gatuna Ajudante]";
		mes "É, eu sei como é, eu também tive que ir naquele lugar horroroso.";
		close;
	}
	if (job_thief_q == 2) {
		mes "[Gatuna Ajudante]";
		mes "Ei, o que você ainda está fazendo aqui? Você deveria estar procurando cogumelos?";
		next;
		if  (select("Sim.:Não, está bem.") == 1) {
			mes "[Gatuna Ajudante]";
			mes "É, sempre tem aqueles mais lentinhos... Pode deixar que eu te explico, mas só essa vez hein!";
			next;
			mes "[Gatuna Ajudante]";
			mes "O teste pra se tornar um Gatuno é o seguinte: Você deve roubar cogumelos da fazenda do 'Shibu', um dos maiores mercadores de Morroc.";
			mes "Você deve colher dois tipos de cogumelos: os ^0000FFCogumelos Venenosos^000000 e os  ^0000FFCogumelos Borrachentos^000000.";
			next;
			mes "[Gatuna Ajudante]";
			mes "Sabia que existem monstros terríveis guardando a fazenda? É isso aí, é hora de você largar a mamadeira e se preparar, essa vai ser a experiência mais difícil da sua vida!";
			next;
			mes "[Gatuna Ajudante]";
			mes "Quando você já tiver colhido os cogumelos, iremos contar os seus pontos... São 3 pontos para cada 1 Cogumelo Venenoso, e 1 ponto para cada Cogumelo Borrachento. Quando você tiver pelo menos 25 pontos, eu deixo você virar um Gatuno.";
			next;
			mes "[Gatuna Ajudante]";
			mes "Saia daqui e ande na direção leste. Procure pelo nosso camarada, ele vai estar entre duas colunas.";
			mes "Fale com ele. Ele vai te colocar para dentro da fazenda usando uma passagem secreta";
			next;
			mes "[Gatuna Ajudante]";
			mes "Hmmm... acho que eu me lembro das coordernadas dele, são  '^FF0000141, 125^000000'.";
			mes "Você sabe o que são coordenadas? Elas te dizem qual a sua localização no momento, é só teclar '/where'";
		close;
		}
		mes "[Gatuna Ajudante]";
		mes "Huh. Por um segundo, eu pensei que você tinha algo realmente importante para me dizer.";
		close;
	}
	mes "[Gatuna Ajudante]";
	if (job_thief_q == 0) {
		mes "Heheheheh... você parece ser um menino limpinho, o que te trouxe para um lugar fedido e sujo como esse? Ah... Então você voltou... Dessa vez é pra valer?";
	}
	else {
		mes "Ah...";
		mes "Você voltou.";
		mes "Tem certeza de que está";
		mes "pronto para tentar novamente?";
	}
	next;
	if (select("Sim, estou pronto para ser um Gatuno!:Err... não, eu acho que não...") == 1) {
		if (job_thief_q == 0) {
			mes "[Gatuna Ajudante]";
			mes "Opa, calma aí... Ser um Gatuno não é algo para se orgulhar, e muito menos sair exibindo por aí...";
			mes "Você tem que tomar cuidado para não ser pego! Por que você não vai e pinta uma placa escrito 'Eu Quero Roubar! Ser um Gatuno e Roubar as Coisas Dos Outros.' e pendura no pescoço?";
			next;
			mes "[Gatuna Ajudante]";
			mes "Então você quer mesmo ser um Gatuno, hein?";
			next;
			switch(select("Sim, eu quero!:Falando serio.. Não.:E você, espertalhona?")) {
		case 1:
			mes "[Gatuna Ajudante]";
			mes "Uau, verdade? Isso é meio estranho...";
			break;
		case 2:
			mes "[Gatuna Ajudante]";
			mes "Então o que você tá fazendo aqui? Tá namorando alguém aqui da Guilda ou resolveu imaginar como seria legal se você fosse uma Gatuna como eu?";
			break;
		case 3:
			mes "[Gatuna Ajudante]";
			mes "Eu? Eu sou a Espertalhona? Bem, fique sabendo que eu não tive escolha, era roubar ou morrer de fome...";
			break;
			}
			next;
			mes "[Gatuna Ajudante]";
			mes "Afinal, você quer mesmo ser um Gatuno?";
			next;
			switch(select("Sim!:Não, meus pais não deixaram.")) {
			case 1:
				mes "[Gatuna Ajudante]";
				mes "Alright, tell";
				mes "me your name.";
				next;
				mes "[Gatuna Ajudante]";
				mes "Qual é o seu nome";
				next;
				mes "[Gatuna Ajudante]";
				mes strcharinfo(0)+", é isso mesmo?";
				job_thief_q = 1;
				next;
				mes "[Gatuna Ajudante]";
				mes "Ok, sua inscrição está pronta.";
				mes "Nós já podemos fazer o teste se você se sentir preparado...";
				mes "Tentar não custa nada! Quer fazer agora?";
				next;
				switch(select("Quero tentar.:Ainda não estou pronto.")) {
				case 1:
					break;
				case 2:
					mes "[Gatuna Ajudante]";
					mes "Você que sabe... Volte quando estiver mais seguro de si!";
					close;
				}
				break;
			case 2:
				mes "[Gatuna Ajudante]";
				mes "Demasiado assustado?!?";
				mes "Hahahahahahah!";
				mes "Oh, por favor ...!";
				mes "Isso é hilário!";
				close;
			}
		}
		else {
			mes "[Gatuna Ajudante]";
			mes "Okay...";
			mes "Me dê";
			mes "um segundo.";
			next;
		}
		mes "[Gatuna Ajudante]";
		mes "O seu nome é "+strcharinfo(0)+"? Certo, vamos ver onde está a sua inscrição...";
		mes "Ah! Aqui está! Nome "+strcharinfo(0)+" Vaaaaaaaaamos ver....";
		next;
		mes "[Gatuna Ajudante]";
		if (getskilllv("NV_BASIC") < 9) {
		mes "Humm... você é bem ambicioso hein?";
		mes "Apressadinho hein?! Volte quando tiver o nível necessário.";
		close;
		}
		mes "Hum...";
		mes "Eu vi a sua Ficha Policial e fiquei sabendo de sua fama nas ruas de Morroc, você deve ser capaz de roubar como um bom Gatuno...";
		next;
		mes "[Gatuna Ajudante]";
		mes "Ok, então você, passou na entrevista pessoal. Parabéns!";
		job_thief_q = 2;
		setquest 1013;
		next;
		mes "[Gatuna Ajudante]";
		mes "Agora vamos testar se você é mesmo capaz de roubar...";
		mes "Você sabe como é o teste?";
		next;
		switch(select("Sim, eu sei.:Não, eu não sei...")) {
		case 1:
			mes "[Gatuna Ajudante]";
			mes "Sabe mesmo?";
			mes "Ótimo, assim as coisas ficam mais rápidas.";
			break;
		case 2:
			mes "[Gatuna Ajudante]";
			mes "Alright, let me inform you then. Listen carefully. This test decides if you are worthy of becoming a Thief.";
			next;
			mes "[Gatuna Ajudante]";
			mes "You will be sneaking to Shibu's Farm. He is the worst Merchant, in terms of character, in Morroc.";
			next;
			mes "[Gatuna Ajudante]";
			mes "Alright, for your test, you gotta steal Mushrooms from his farm. Don't worry, that guy deserves to be robbed.";
			next;
			mes "[Gatuna Ajudante]";
			mes "Anyway, you gotta gather two kinds of Mushrooms: ^0000FFOrange Net Mushrooms^000000 and ^0000FFOrange Gooey Mushrooms^000000.";
			next;
			mes "[Gatuna Ajudante]";
			mes "Be careful, since there are monsters are the farm that are there to protect the Mushrooms. So this will be no walk in the park.";
			next;
			mes "[Gatuna Ajudante]";
			mes "When you come back here after gathering Mushrooms, you'll be graded on the Mushrooms you've collected.";
			next;
			mes "[Gatuna Ajudante]";
			mes "Each Orange Net Mushroom gets you 3 points, and you get 1 point for each Orange Gooey Mushroom. You need a total of 25 points to pass the test.";
			next;
			mes "[Gatuna Ajudante]";
			mes "Go outside and keep going ahead toward the Eastern Field of the Pyramids. Then you will see one of our comrades between two columns.";
			next;
			mes "[Gatuna Ajudante]";
			mes "Speak to that guy, and he'll take you to the farm through the backdoor.";
			next;
			mes "[Gatuna Ajudante]";
			mes "On that field, I think his coordinates are '^FF0000141, 125^000000.' Just type ^3355FF/where^000000 in the right side of your chat box to check your present coordinates.";
		}
		next;
		mes "[Gatuna Ajudante]";
		mes "Don't forget to make plans and prepare yourself before you go inside the Mushroom Farm. Move as quickly as you can and try not to get killed, alright?";
		close;
	}
	close;
}

moc_prydb1,42,133,2	script	Comrade	2_M_THIEFMASTER,{
	if (BaseJob == Job_Thief) {
		mes "[Brad]";
		mes "We don't have any special events yet. Come some other time when there's news, alright?";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[Comrade]";
		mes "Um...";
		mes "You don't look";
		mes "like a Thief.";
		next;
		mes "[Comrade]";
		mes "What the heck are";
		mes "you doing here anyway?";
		close;
	}
	if (job_thief_q == 0 || job_thief_q == 1) {
		mes "[Comrade]";
		mes "What's the matter? If you want to be a Thief, speak to the girl beside me.";
		close;
	}
	else if (job_thief_q == 2) {
		mes "[Comrade]";
		mes "Did you pass the interview?";
		mes "Then what are you waiting for?";
		close;
	}
	else if (job_thief_q == 3) {
		mes "[Comrade]";
		mes "Ah, the guide told me about you. So, let me check your mushrooms...";
		if (countitem(1069) == 0 && countitem(1070) == 0) {
			next;
			mes "[Comrade]";
			mes "What the hell...";
			mes "You don't have any Mushrooms at all! Go back and get them. Otherwise, you won't pass the test and become a Thief!";
			close;
		}
		next;
		.@thief_item1 = countitem(1069) * 3;
		.@thief_item2 = countitem(1070);
		.@total_thief = .@thief_item1 + .@thief_item2;
		.@money_thief = ((.@thief_item1 * 5) + (.@thief_item2 * 2)) + 200;
		mes "[Comrade]";
		if (countitem(1069) != 0) {
			mes "First, let me check the Orange Net Mushrooms you got.";
			mes "Huh, " + countitem(1069) + " of them.";
		}
		if (countitem(1070) != 0)
			mes "Now I'll just check your Orange Gooey Mushrooms. That's " + countitem(1070) + " you gathered.";
		next;
		mes "[Comrade]";
		mes "So that would";
		mes "bring your total to...";
		next;
		mes "[Comrade]";
		mes "Hmmm. " + .@total_thief + " degrees, multiplied by the speed of light, divided by the integral of pi times height plus the absolute value of politics...";
		next;
		mes "[Comrade]";
		mes "Okay!";
		mes "I got it.";
		next;
		mes "[Comrade]";
		if (.@total_thief > 25) {
			mes "You got more";
			mes "than 25 points!";
			mes "Awesome!";
		}
		else if (.@total_thief == 25) {
			mes "Exactly 25 points!";
			mes "You did it! Badass!";
		}
		else {
			mes "Definitely less than the 25 points you need to pass. Go out there and get me more Mushrooms!";
			close;
		}
		next;
		mes "[Comrade]";
		mes strcharinfo(0) + "...";
		mes "You have passed the official Thief Test. You are now one of us.";
		if (countitem(1069) != 0) delitem 1069,countitem(1069); //Mushroom_Of_Thief_1
		if (countitem(1070) != 0) delitem 1070,countitem(1070); //Mushroom_Of_Thief_2
		callfunc "Job_Change",Job_Thief;
		callfunc "F_ClearJobVar";
		completequest 1013;
		next;
		mes "[Comrade]";
		mes "Congratulations on becoming a Thief! From now, be an honorable representative of the Thief's Guild.";
		next;
		mes "[Comrade]";
		mes "If you bring disgrace to our guild, you will be killed. I expect you to bring our comrades pride.";
		next;
		mes "[Brad]";
		mes "*Ahem* Welcome to the Guild, comrade! I'm Brad, and I'm in charge of human resources here.";
		next;
		mes "[Brad]";
		mes "Here is a small subsidy for a Newbie like you. Spend it whereever you want. Alright then, I'll see you around~";
		Zeny += .@money_thief;
		close;
	}
}

moc_ruins,141,125,3	script	Mr. Irrelevant	4_M_01,{
	if (BaseJob == Job_Thief) {
		mes "[Mr. Irrelevant]";
		mes "Ah, I see that you are now a Thief. I always knew you'd join us.";
		next;
		mes "[Mr. Irrelevant]";
		mes "Stealing from a Mushroom farm is too easy for you now. You should build up your skills and master our craft.";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[Mr. Irrelevant]";
		switch(rand(4)) {
		case 1:
			mes "I could use a good, hard drink.";
			close;
		case 2:
			mes "Gimme your money.";
			next;
			mes "[Mr. Irrelevant]";
			mes "Kidding, I'm off the clock.";
			close;
		case 3:
			mes "WHO YOU CALLING A PSYCHO?!?!";
			close;
		case 4:
			mes "I've got nothing to say to you. Would you mind leaving me alone?";
			close;
		default:
			mes "Today looks like a good day to go to the pyramids and hunt with some of my friends.";
			close;
		}
	}
	if (job_thief_q == 3) {
		mes "[Mr. Irrelevant]";
		mes "Hahahahaha~!";
		mes "You haven't";
		mes "passed the test yet?";
		mes "Alright, I'll let you in...";
		close2;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 2) {
		mes "[Mr. Irrelevant]";
		mes "Hmm...";
		mes "You've come to take the test, right? I can see in your eyes that you know something.";
		next;
		mes "[Mr. Irrelevant]";
		mes "Your name is " + strcharinfo(0) + "? Ah, it's on the list. Alright, I'll let you into the Mushroom Farm , but I can't guarantee your safety...";
		close2;
		job_thief_q = 3;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 1) {
		mes "[Mr. Irrelevant]";
		mes "There is this strange smell coming from... You. Now why would that be?";
		close;
	}
	mes "[Mr. Irrelevant]";
	mes "Hey Novice! Why don't you join the ranks of the Thief Guild? You newbies are always welcome to join us and our selfish cause.";
	next;
	mes "[Mr. Irrelevant]";
	mes "You can get more information in the Underground Room in the Pyramid 1 BF.";
	close;
}

// Monsters
//============================================================
job_thief1,0,0,0,0	monster	Orange Mushroom	1182,180,0,0,0
job_thief1,0,0,0,0	monster	Fabre	1184,50,0,0,0
job_thief1,0,0,0,0	monster	Chonchon	1183,50,0,0,0
job_thief1,0,0,0,0	monster	Spore	1014,30,0,0,0

//============================================================
// mapflag
//============================================================
