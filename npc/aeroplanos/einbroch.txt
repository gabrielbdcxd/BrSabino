//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: einbroch.txt                           |
//   |--------------------------------------------------------|
//   | Criado por: eA/Herc/brA Dev. Team                      |
//   |--------------------------------------------------------|
//   | Versões eA/Herc/brA: ?.?/1.0/1.0                       |
//   |--------------------------------------------------------|
//   | Descrição: Aeroplano de Einbroch                       |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Cabeçalho brAthena [Axl]                           |
//   |--------------------------------------------------------|
//   | - Revisado: [Jeeffrey]                                 |
//   \________________________________________________________/

airport,143,43,5	script	Assistente do Aeroplano#airport1a::airport1	4_F_01,{
	mes "[Assistente do Aeroplano]";
	mes "Bem-vindo ao Aeroporto de Einbroch, onde oferecemos constantes voos para as cidades de Juno, Lighthalzen e Hugel.";
	next;
	if (select("Embarcar no Dirigível:Cancelar") == 1) {
		mes "[Assistente do Aeroplano]";
		mes "A taxa de embarque do Dirigível é 1,200 zeny, mas se você possuir um Bilhete para Aeroplano, a taxa não será cobrada.";
		mes "Você deseja embarcar no Dirigível?";
		next;
		if (select("Sim:Não") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; // Bilhete para Aeroplano
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				Zeny -= 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Assistente do Aeroplano]";
			mes "Me desculpe, mas você não possui um Bilhete para Aeroplano e você não possui a quantidade de zenys suficientes para embarcar.";
			mes "Lembre, a taxa de embarque é 1,200 zeny.";
			close;
		}
	}
	mes "[Assistente do Aeroplano]";
	mes "Obrigado e tenha um bom dia.";
	close;
}

airport,158,43,5	duplicate(airport1)	Assistente do Aeroplano#airport1b	4_F_01
airport,126,43,5	duplicate(airport1)	Assistente do Aeroplano#airport1c	4_F_01

airport,143,49,3	script	Equipe de Recepção#airport2a::airport2	4_F_01,{
	mes "[Equipe de Recepção]";
	mes "Bem-vindo ao Aeroporto de Einbroch.";
	mes "Se você acaba de chegar do seu voo, deixe-me guiá-lo até seu terminal principal.";
	mes "Caso contrário, por favor embarque no Dirigível para ir até Juno, Lighthalzen ou Hugel.";
	next;
	if (select("Sair do terminal principal.:Cancelar.") == 1) {
		mes "[Equipe de Recepção]";
		mes "Uma vez no terminal principal, você terá que pagar a taxa novamente para embarcar no Dirigível.";
		mes "Você só deve sair se a cidade de Einbroch for seu destino final.";
		mes "Deseja prosseguir para o terminal principal?";
		next;
		if (select("Sim:Não") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Equipe de Recepção]";
	mes "Certo, obrigado pelo seu apoio, e espero que você tenha um voo agradável~";
	close;
}

airport,126,51,3	duplicate(airport2)	Equipe de Recepção#airport2b	4_F_01
airport,158,50,3	duplicate(airport2)	Equipe de Recepção#airport2c	4_F_01

einbroch,94,267,3	script	Assistente do Aeroplano#ein01	4_F_02,{
	mes "[Assistente do Aeroplano]";
	mes "Bem-vindo ao Aeroporto de Einbroch.";
	mes "Por favor utilize esta porta para embarcar no dirigível que irá partir até Juno, Lighthalzen e Hugel na República de Schwarzwald.";
	next;
	mes "[Assistente do Aeroplano]";
	mes "Caso contrário, se é Einbroch seu destino pretendido, por favor, desça as escadas e perça a Equipe de Recepção para levar você até o terminal.";
	mes "Te agradeço, e tenha uma boa viagem.";
	close;
}
